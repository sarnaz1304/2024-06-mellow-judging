Recumbent Iris Goldfish

High

# Withdrawal Fee Affects Deposit/Withdrawal Ratios, Allowing for More Profitable Returns Based on Withdrawal Order

## Summary
The protocol has a withdrawal fee mechanism that takes a percentage from token withdrawals. However, tokens that remain inside the contract will affect lpAmount ratio because the total supply will decrease, and the TVL of the token won't decrease proportionally.

## Vulnerability Detail
The function analyzeRequest inside Vault.sol calculates withdrawal amounts with the deduction of feeD9. This fee will remain inside the Vault contract after withdrawals are completed. However, because ratio calculations are based on checking the Vault's balance manually via ERC20's balanceOf function (https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/modules/erc20/ERC20TvlModule.sol#L17-L18), we encounter a problem. The total supply will decrease according to the specified lpAmount by the user, but the token amounts inside the contract won't decrease proportionally.

## Impact
This means that users who withdraw last will receive the most tokens for an equal amount of LP tokens. This occurs because the operator processes all users withdrawals with several transactions if many users decide to withdraw their funds within a short time frame.

### POC
1. Update vault setUp function to set feeD9 to maximum value 5e7 (VaultTestCommon.t.sol)
```solidity
    function _setUp(Vault vault) internal {
        ERC20TvlModule erc20TvlModule = new ERC20TvlModule();
        vault.addTvlModule(address(erc20TvlModule));

        vault.addToken(Constants.WSTETH);
        vault.addToken(Constants.RETH);
        vault.addToken(Constants.WETH);
        // investigate fee d9 
        VaultConfigurator configurator = VaultConfigurator(
            address(vault.configurator())
        );

        // oracles setup
        {
            ManagedRatiosOracle ratiosOracle = new ManagedRatiosOracle();

            uint128[] memory ratiosX96 = new uint128[](3);
            ratiosX96[0] = 2 ** 96;
            ratiosOracle.updateRatios(address(vault), true, ratiosX96);
            ratiosOracle.updateRatios(address(vault), false, ratiosX96);

            configurator.stageRatiosOracle(address(ratiosOracle));
            configurator.commitRatiosOracle();
            configurator.stageWithdrawalFeeD9(5e7); // ADDED
            configurator.commitWithdrawalFeeD9(); // ADDED
            ChainlinkOracle chainlinkOracle = new ChainlinkOracle();
            chainlinkOracle.setBaseToken(address(vault), Constants.WSTETH);
            address[] memory tokens = new address[](3);
            tokens[0] = Constants.WSTETH;
            tokens[1] = Constants.RETH;
            tokens[2] = Constants.WETH;

            IChainlinkOracle.AggregatorData[]
                memory data = new IChainlinkOracle.AggregatorData[](3);
            data[0] = IChainlinkOracle.AggregatorData({
                aggregatorV3: address(
                    new WStethRatiosAggregatorV3(Constants.WSTETH)
                ),
                maxAge: 30 days
            });
            data[1] = IChainlinkOracle.AggregatorData({
                aggregatorV3: Constants.RETH_CHAINLINK_ORACLE,
                maxAge: 30 days
            });
            data[2] = IChainlinkOracle.AggregatorData({
                aggregatorV3: address(new ConstantAggregatorV3(1 ether)),
                maxAge: 30 days
            });
            chainlinkOracle.setChainlinkOracles(address(vault), tokens, data);

            configurator.stagePriceOracle(address(chainlinkOracle));
            configurator.commitPriceOracle();
        }

        configurator.stageMaximalTotalSupply(1000 ether);
        configurator.commitMaximalTotalSupply();
    }
```

insert this test inside e2e/VaultTest.t.sol
```solidity
    function testDepositAndWithdrawRegularr1() external {
        Vault vault = new Vault("Mellow LRT Vault", "mLRT", admin);
        vm.startPrank(admin);
        vault.grantRole(vault.ADMIN_DELEGATE_ROLE(), admin);
        vault.grantRole(vault.OPERATOR(), operator);
        _setUp(vault);
        vm.stopPrank();
        _initialDeposit(vault);

        address depositor = address(bytes20(keccak256("depositor")));
        address depositor2 = address(bytes20(keccak256("depositor2")));
        quickDeposit(depositor2, vault);
        quickDeposit(depositor, vault);

        quickWithdraw(depositor, vault);
        quickWithdraw(depositor2, vault);

@>       console.log(IERC20(Constants.WSTETH).balanceOf(depositor)); // 9500000000000000000
@>       console.log(IERC20(Constants.WSTETH).balanceOf(depositor2)); // 9974999999525000000, but they both deposited the same amount of WSTETH
    }


    function quickWithdraw(address depositor, Vault vault) internal {
        address[] memory users = new address[](1);
        users[0] = depositor;
        vm.startPrank(operator);
        vault.processWithdrawals(users);
        vm.stopPrank();
    }

    function quickDeposit(address depositor, Vault vault) internal {
        vm.startPrank(depositor);

        deal(Constants.WSTETH, depositor, 10 ether);
        IERC20(Constants.WSTETH).safeIncreaseAllowance(
            address(vault),
            10 ether
        );

        uint256[] memory amounts = new uint256[](3);
        amounts[0] = 10 ether;

        vault.deposit(depositor, amounts, 1, type(uint256).max, 0);
        console.log(vault.balanceOf(depositor));
        vm.stopPrank();

        uint256[] memory minAmounts = new uint256[](3);
        minAmounts[0] = 9 ether;
        minAmounts[1] = 0 ether;
        minAmounts[2] = 0 ether;
        vm.startPrank(depositor);
        vault.registerWithdrawal(
            depositor,
            10 ether,
            minAmounts,
            type(uint256).max,
            type(uint256).max,
            false
        );
        vm.stopPrank();
    }
```


## Code Snippet
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/Vault.sol#L486

## Tool used
Foundry

## Recommendation
Send tokens remainder to some FEE_TAKER wallet/contract or change ERC20TvlModule logic to manually calculate erc20 balances.
