Rapid Fuchsia Cuckoo

High

# The withdrawal fee continues to accumulate and is not excluded from the vault's total value, which will affect subsequent deposits and withdrawals.

## Summary
The withdrawal fee continues to accumulate and is not excluded from the vault's `totalValue`, which will affect the calculation of the amount of LP tokens when depositing and the calculation of the amount of underlying tokens when withdrawing.

## Vulnerability Detail
If the withdrawal fee is not zero, the protocol will charge some fees to process the withdrawals. The withdrawal fees are deducted from the value represented by the `lpAmount` LP tokens. They are retained in the vault and are part of the underlying tokens.
```solidity
// Vault.sol#analyzeRequest()
476:    function analyzeRequest(
477:        ProcessWithdrawalsStack memory s,
478:        WithdrawalRequest memory request
479:    ) public pure returns (bool, bool, uint256[] memory expectedAmounts) {
480:        uint256 lpAmount = request.lpAmount;
481:        if (
482:            request.tokensHash != s.tokensHash || request.deadline < s.timestamp
483:        ) return (false, false, expectedAmounts);
484:
485:@>      uint256 value = FullMath.mulDiv(lpAmount, s.totalValue, s.totalSupply);
486:@>      value = FullMath.mulDiv(value, D9 - s.feeD9, D9);
487:        uint256 coefficientX96 = FullMath.mulDiv(value, Q96, s.ratiosX96Value);
488:
489:        uint256 length = s.erc20Balances.length;
490:        expectedAmounts = new uint256[](length);
491:        for (uint256 i = 0; i < length; i++) {
492:            uint256 ratiosX96 = s.ratiosX96[i];
493:@>          expectedAmounts[i] = ratiosX96 == 0
494:@>              ? 0
495:@>              : FullMath.mulDiv(coefficientX96, ratiosX96, Q96);
496:            if (expectedAmounts[i] >= request.minAmounts[i]) continue;
497:            return (false, false, expectedAmounts);
498:        }
499:        for (uint256 i = 0; i < length; i++) {
500:            if (s.erc20Balances[i] >= expectedAmounts[i]) continue;
501:            return (true, false, expectedAmounts);
502:        }
503:@>      return (true, true, expectedAmounts);
504:    }
```
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/Vault.sol#L476-L504

During the processes of depositing and withdrawing, the protocol calculates the vault's `totalValue`. The vault's `totalValue` is the sum of all underlying tokens' amounts multiplied by their respective prices. Since the withdrawal fees are part of the underlying tokens, they are included in the calculation, which results in a `totalValue` that is larger than it should be. The incorrect `totalValue` will affect subsequent deposits and withdrawals.
```solidity
// Vault.sol#deposit()
302:        (
303:            address[] memory tokens,
304:            uint256[] memory totalAmounts
305:@>      ) = underlyingTvl();
            ...
318:        uint256 depositValue = 0;
319:        uint256 totalValue = 0;
320:        actualAmounts = new uint256[](tokens.length);
321:        IPriceOracle priceOracle = IPriceOracle(configurator.priceOracle());
322:        for (uint256 i = 0; i < tokens.length; i++) {
323:            uint256 priceX96 = priceOracle.priceX96(address(this), tokens[i]);
324:@>          totalValue += totalAmounts[i] == 0
325:@>              ? 0
326:@>              : FullMath.mulDivRoundingUp(totalAmounts[i], priceX96, Q96);
                ...
336:        }
```
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/Vault.sol#L302-L336

```solidity
// Vault.sol#calculateStack()
512:@>      (address[] memory tokens, uint256[] memory amounts) = underlyingTvl();
            ...
527:        for (uint256 i = 0; i < tokens.length; i++) {
528:            uint256 priceX96 = priceOracle.priceX96(address(this), tokens[i]);
529:@>          s.totalValue += FullMath.mulDiv(amounts[i], priceX96, Q96);
530:            s.ratiosX96Value += FullMath.mulDiv(s.ratiosX96[i], priceX96, Q96);
531:            s.erc20Balances[i] = IERC20(tokens[i]).balanceOf(address(this));
532:        }
```
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/Vault.sol#L512-L532

### POC:
1. Add some logs to display intermediate values during the deposit and withdrawal operations.
```solidity
// Vault.sol#_processLpAmount()
    function _processLpAmount(
        address to,
        uint256 depositValue,
        uint256 totalValue,
        uint256 minLpAmount
    ) private returns (uint256 lpAmount) {
        uint256 totalSupply = totalSupply();
+       console2.log("_processLpAmount: depositValue=%d, totalValue=%d, totalSupply=%d", depositValue, totalValue, totalSupply);
        if (totalSupply == 0) {
            // scenario for initial deposit
            _requireAtLeastOperator();
            lpAmount = minLpAmount;
+           console2.log("_processLpAmount lpAmount=%d, minLpAmount=%d", lpAmount, minLpAmount);
            ...
        } else {
            lpAmount = FullMath.mulDiv(depositValue, totalSupply, totalValue);
+           console2.log("_processLpAmount lpAmount=%d, minLpAmount=%d", lpAmount, minLpAmount);
            ...
        }
        ...
    }

// Vault.sol#analyzeRequest()
    function analyzeRequest(
        ProcessWithdrawalsStack memory s,
        WithdrawalRequest memory request
    ) public pure returns (bool, bool, uint256[] memory expectedAmounts) {
        ...
        uint256 value = FullMath.mulDiv(lpAmount, s.totalValue, s.totalSupply);
+       console2.log("analyzeRequest: lpAmount=%d, totalValue=%d, totalSupply=%d", lpAmount, s.totalValue, s.totalSupply);
        value = FullMath.mulDiv(value, D9 - s.feeD9, D9);
+       console2.log("analyzeRequest: feeD9=%d, value=%d", s.feeD9, value);
        uint256 coefficientX96 = FullMath.mulDiv(value, Q96, s.ratiosX96Value);
        ...
    }
```

2. In `VaultTestCommon.t.sol#_setUp()`, we set the withdrawal fee to 5e7 (i.e. 5%).
```solidity
// VaultTestCommon.t.sol#_setUp()
    function _setUp(Vault vault) internal {
        ...
            configurator.stageRatiosOracle(address(ratiosOracle));
            configurator.commitRatiosOracle();

+           configurator.stageWithdrawalFeeD9(5e7);
+           configurator.commitWithdrawalFeeD9();
```

3. Add the following test case `testDepositAndWithdrawIssue()` to `VaultTest.t.sol`.
```solidity
// VaultTest.t.sol
    function testDepositAndWithdrawIssue() external {
        Vault vault = new Vault("Mellow LRT Vault", "mLRT", admin);
        vm.startPrank(admin);
        vault.grantRole(vault.ADMIN_DELEGATE_ROLE(), admin);
        vault.grantRole(vault.OPERATOR(), operator);
        _setUp(vault);
        vm.stopPrank();
        _initialDeposit(vault);

        uint256[] memory amounts = new uint256[](3);
        amounts[0] = 10 ether;
        uint256[] memory minAmounts = new uint256[](3);

        address depositor1 = address(bytes20(keccak256("depositor1")));
        address depositor2 = address(bytes20(keccak256("depositor2")));

        // *depositor1* deposits 10 ether and registers withdrawal
        {
            vm.startPrank(depositor1);
            deal(Constants.WSTETH, depositor1, 10 ether);
            IERC20(Constants.WSTETH).safeIncreaseAllowance(address(vault), 10 ether);

            console2.log("\ndepositor1 deposits 10 ether:");
            vault.deposit(depositor1, amounts, 0, type(uint256).max);
            assertEq(IERC20(Constants.WSTETH).balanceOf(depositor1), 0);
            assertEq(vault.balanceOf(depositor1), 10 ether);

            vault.registerWithdrawal(depositor1, 10 ether, minAmounts, type(uint256).max, type(uint256).max, false);
            vm.stopPrank();
        }

        // *operator* processes the withdrawal of depositor1
        {
            vm.startPrank(operator);
            address[] memory withdrawers1 = vault.pendingWithdrawers();
            assertEq(withdrawers1.length, 1);
            assertEq(withdrawers1[0], depositor1);

            console2.log("\noperator processes depositor1's withdraw:");
            vault.processWithdrawals(withdrawers1);
            assertEq(IERC20(Constants.WSTETH).balanceOf(depositor1), 9.5 ether);  // withdraw fee is 0.5 ether
            assertEq(vault.balanceOf(depositor1), 0);
            vm.stopPrank();
        }

        // *depositor2* deposits 10 ether and registers withdrawal
        {
            vm.startPrank(depositor2);
            deal(Constants.WSTETH, depositor2, 10 ether);
            IERC20(Constants.WSTETH).safeIncreaseAllowance(address(vault), 10 ether);

            console2.log("\ndepositor2 deposits 10 ether:");
            vault.deposit(depositor2, amounts, 0, type(uint256).max);  // set minLpAmount to 0 to see how many LP tokens are minted
            assertEq(IERC20(Constants.WSTETH).balanceOf(depositor2), 0);
            assertLt(vault.balanceOf(depositor2), 10 ether);
            console2.log("LP amount of depositor2: %d", vault.balanceOf(depositor2));

            vault.registerWithdrawal(depositor1, vault.balanceOf(depositor2), minAmounts, type(uint256).max, type(uint256).max, false);
            vm.stopPrank();
        }

        // *operator* processes the withdrawal of depositor2
        {
            vm.startPrank(operator);
            address[] memory withdrawers2 = vault.pendingWithdrawers();
            assertEq(withdrawers2.length, 1);
            assertEq(withdrawers2[0], depositor2);

            console2.log("\noperator processes depositor2's withdraw:");
            vault.processWithdrawals(withdrawers2);

            assertLt(IERC20(Constants.WSTETH).balanceOf(depositor2), 9.5 ether);
            assertEq(vault.balanceOf(depositor2), 0);
            console2.log("WSTETH amount of depositor2: %d", ERC20(Constants.WSTETH).balanceOf(depositor2));
            vm.stopPrank();
        }
    }
```

4. Run test case `testDepositAndWithdrawIssue()`, and the logs are as follows. 
```solidity
 1:    Ran 1 test for tests/mainnet/e2e/VaultTest.t.sol:VaultTestE2E
 2:    [PASS] testDepositAndWithdrawIssue() (gas: 13125597)
 3:    Logs:
 4:      _processLpAmount: depositValue=10000000000, totalValue=0, totalSupply=0
 5:      _processLpAmount lpAmount=10000000000, minLpAmount=10000000000
 6:      
 7:    depositor1 deposits 10 ether:
 8:@*    _processLpAmount: depositValue=10000000000000000000, totalValue=10000000000, totalSupply=10000000000
 9:      _processLpAmount lpAmount=10000000000000000000, minLpAmount=0
10:      
11:    operator processes depositor1's withdraw:
12:@=    analyzeRequest: lpAmount=10000000000000000000, totalValue=10000000010000000000, totalSupply=10000000010000000000
13:      analyzeRequest: feeD9=50000000, value=9500000000000000000
14:      
15:    depositor2 deposits 10 ether:
16:@*    _processLpAmount: depositValue=10000000000000000000, totalValue=500000010000000000, totalSupply=10000000000
17:      _processLpAmount lpAmount=199999996000, minLpAmount=0
18:@>    LP amount of depositor2: 199999996000
19:      
20:    operator processes depositor2's withdraw:
21:@=    analyzeRequest: lpAmount=199999996000, totalValue=10500000010000000000, totalSupply=209999996000
22:      analyzeRequest: feeD9=50000000, value=9499999999999999818
23:@>    WSTETH amount of depositor2: 0
24:    
25:    Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 9.00ms (8.08ms CPU time)
26:    
27:    Ran 1 test suite in 859.52ms (9.00ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```
As the logs show, `depositor1` deposited `10 ether` and received `10 ether LP tokens`, and then received `9.5 ether WSTETH tokens` after withdrawal (withdrawal fee is `0.5 ether`). However, `depositor2` also deposited `10 ether`, but received only `199999996000 LP tokens` (L18 in logs), and received `0 WSTETH tokens` after withdrawal (L23 in logs, due to rounding down to 0).

The reason for the issue is that the withdrawal fee from `depositor1` was retained in the vault, and was not excluded when calculating the vault's total value in subsequent deposits and withdrawals (see the `totalValue` in the logs, `L16` vs `L8`, `L21` vs `L12`).

## Impact
When calculating the vault's `totalValue`, the accumulated withdrawal fees are not excluded, resulting in a `totalValue` that is larger than it should be. The impacts include:

1. When depositing, users receive fewer LP tokens than they should (`L360`: the *denominator* is larger than it should be).
```solidity
// Vault.sol#_processLpAmount()
360:            lpAmount = FullMath.mulDiv(depositValue, totalSupply, totalValue)
```
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/Vault.sol#L360

2. When withdrawing, users receive more tokens than they should (`L485`: the *numerator* is larger than it should be).
```solidity
// Vault.sol#analyzeRequest()
485:        uint256 value = FullMath.mulDiv(lpAmount, s.totalValue, s.totalSupply);
486:        value = FullMath.mulDiv(value, D9 - s.feeD9, D9);
```
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/Vault.sol#L485-L486

## Code Snippet
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/Vault.sol#L302-L336

https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/Vault.sol#L512-L532

## Tool used

Manual Review

## Recommendation
There are two methods to fix this issue:
1. During processing withdrawals, transfer the withdrawal fees to a fee recipient address.
2. Use a storage variable to record the accumulated withdrawal fees and exclude them from the calculation of the vault's `totalValue`.