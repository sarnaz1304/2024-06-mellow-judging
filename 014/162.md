Happy Bone Stallion

High

# User may not receive profit from withdrawal fee as expected and attacker can steal value from pool

## Summary
When a user's withdrawal request is processed, the user is charged a withdrawal fee. The withdrawal fee increases the value of the LP tokens, and the LP token holders earns profit from it. 

However, when multiple withdrawal requests are processed in a batch, the single withdrawal contributes no value to LP token until the end of the process, user may not receive profit as expected, and the value can be steal by an attacker through sandwich attack.

## Vulnerability Detail
It is expected that a user earn profit from the withdrawal fee paid by other users, for example, when `userA` withdraws, the withdrawal fee is charged and LP token becomes more valuable, then when `userB` withdraws, they may withdraw more tokens than they deposited.

However, users earn no profit from withdrawal fee if their withdrawal requests are processed in a batch. When [multiple withdrawal requests are processed in a batch](https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/Vault.sol#L536-L538), the state stack required for processing withdrawal requests is [calculated](https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/Vault.sol#L507-L510):
```solidity
    function calculateStack()
        public
        view
        returns (ProcessWithdrawalsStack memory s)
    {
        (address[] memory tokens, uint256[] memory amounts) = underlyingTvl();
        s = ProcessWithdrawalsStack({
            tokens: tokens,
            ratiosX96: IRatiosOracle(configurator.ratiosOracle())
                .getTargetRatiosX96(address(this), false),
            erc20Balances: new uint256[](tokens.length),
@>          totalSupply: totalSupply(),
            totalValue: 0,
            ratiosX96Value: 0,
            timestamp: block.timestamp,
            feeD9: configurator.withdrawalFeeD9(),
            tokensHash: keccak256(abi.encode(tokens))
        });

        ...
    }
```
The state stack is then passed to [analyzeRequest()](https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/Vault.sol#L476-L479) to calculate the expected token amount for each request.
```solidity
    function analyzeRequest(
        ProcessWithdrawalsStack memory s,
        WithdrawalRequest memory request
    ) public pure returns (bool, bool, uint256[] memory expectedAmounts) {
        ...

@>      uint256 value = FullMath.mulDiv(lpAmount, s.totalValue, s.totalSupply);
@>      value = FullMath.mulDiv(value, D9 - s.feeD9, D9);
@>      uint256 coefficientX96 = FullMath.mulDiv(value, Q96, s.ratiosX96Value);

        ...

        for (uint256 i = 0; i < length; i++) {
            uint256 ratiosX96 = s.ratiosX96[i];
@>          expectedAmounts[i] = ratiosX96 == 0 ? 0 : FullMath.mulDiv(coefficientX96, ratiosX96, Q96);
            if (expectedAmounts[i] >= request.minAmounts[i]) continue;
            return (false, false, expectedAmounts);
        }

       ...

        return (true, true, expectedAmounts);
    }
```
To simplify,  the expected token amount is calculated as below:
> expectedAmounts = ((lpAmount * s.totalValue / s.totalSupply) / s.ratiosX96Value) * ratiosX96

The problem is that `s.totalSupply` and `s.totalValue` are constant during the calculation process, indicating that no withdrawal fee is contributed to the value of the LP token, even if the fee has been charged. For instance, in the batch `[userA request,  userB request, userC request]`, `userB` won't receive profit from the withdrawal fee paid by `userA`, and `userC` receive no profit from the fee paid by `userA` and `user`B.

The users' LP tokens are burned at the end of the batch withdrawal process, by then the value of the LP token is increased. This can be leveraged by an attacker, who may intentionally front-run a large batch withdrawal to mint LP tokens and then back-run  to withdraw, hence steal the value from the pool.

## Impact
1. User may receive much less profit than expected, or no profit at all;
2. Malicious user can steal withdrawal fee from the pool.

## Code Snippet
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/Vault.sol#L476-L504

## Tool used
Manual Review

## Recommendation
It is expected the withdrawal requests are processed sequentially in the registration time order, so each withdrawal should generate profit to the following ones. This can be achieved by updating `s.totalSupply` and `s.totalValue` in the process of calculating `expectedAmounts`.
```diff
    function analyzeRequest(
        ProcessWithdrawalsStack memory s,
        WithdrawalRequest memory request
    ) public pure returns (bool, bool, uint256[] memory expectedAmounts) {
        ...

        for (uint256 i = 0; i < length; i++) {
-           if (s.erc20Balances[i] >= expectedAmounts[i]) continue;
+           if (s.erc20Balances[i] >= expectedAmounts[i]) {
+               s.totalSupply -= request.amount;
+               s.totalValue -= value;
+               continue;
+           }

            return (true, false, expectedAmounts);
        }
        return (true, true, expectedAmounts);
    }
```
