Attractive Vanilla Shark

High

# `_stethToWsteth(amount)` can revert because it is possible for `steth.safeTransferFrom(...)` to transfer 1 or 2 lesser tokens.

## Summary
The stETh token has a [1 or 2 corner case](https://docs.lido.fi/guides/lido-tokens-integration-guide#1-2-wei-corner-case) which makes `steth.safeTransferFrom(...)`  transfer 1 or 2 lesser tokens than the specified `amount`. This will cause the subsequent executiong `_stethToWsteth(amount)` that expects that the full `amount` is transferred to the `DepositWrapper.sol` contract.
```solidity
File: DepositWrapper.sol
      if (token == steth) {
            IERC20(steth).safeTransferFrom(sender, wrapper, amount);//FINDING: amount - 1 can be transferred to this contract
            amount = _stethToWsteth(amount);//Finding: still using the input amount that is not up to the actual transferred amount.
        }
```
See here for details: https://docs.lido.fi/guides/lido-tokens-integration-guide#1-2-wei-corner-case


## Vulnerability Detail
The main issue is that the `transferFrom(...)` function of the `stEth` has an edge case known as the [1-2 wei corner case](https://docs.lido.fi/guides/lido-tokens-integration-guide#1-2-wei-corner-case).  This simply means the `steth.safeTransferFrom(...)` function of the stEth contract can in some cases transfer 1 or 2 lesser token to the passed input `amount`.

According to the stEth Integration docs:

> stETH balance calculation includes integer division, and there is a common case when the whole stETH balance can't be transferred from the account while leaving the last 1-2 wei on the sender's account. The same thing can actually happen at any transfer or deposit transaction. In the future, when the stETH/share rate will be greater, the error can become a bit bigger. To avoid it, one can use transferShares to be precise.

This is how the above can cause DOS in the `deposit(...)` function of the `DepositWrapper.sol` contract.
Looking at the deposit function below, when a user tries to deposit `stEth` token, the `steth.safeTransferFrom(...)` function is first called to transfer the stEth to the `DepositWrapper.sol` contract before wrapping it with the `_stethToWsteth(amount)` that follows. The issue here is `_stethToWsteth(amount)` assumes that the full `amount` was transferred to the `DepositWrapper.sol` contract meanwhile it is possible that `amount - 1` or  `amount - 2` due to rounding that is explained here in the [docs](https://docs.lido.fi/guides/lido-tokens-integration-guide#1-2-wei-corner-case).

When the token transferred is less than the `amount` passed in `_stethToWsteth(amount)` the transaction will revert because `_stethToWsteth(amount)` tries to `wrap` an `amount` that is less than what was actually transferred.

```solidity
File: DepositWrapper.sol
function deposit(
        address to,
        address token,
        uint256 amount,
        uint256 minLpAmount,
        uint256 deadline
    ) external payable returns (uint256 lpAmount) {
        address wrapper = address(this);
        address sender = msg.sender;
        address[] memory tokens = vault.underlyingTokens();
        if (tokens.length != 1 || tokens[0] != wsteth)
            revert InvalidTokenList();
        if (amount == 0) revert InvalidAmount();
        if (token == steth) {
@>            IERC20(steth).safeTransferFrom(sender, wrapper, amount);//@issue actual tokens transferred may be 1 or 2 less
@>            amount = _stethToWsteth(amount);//@audit here will revert because it tries to wrap amount but what is transferred is not up to amount
        } else if (token == weth) {
            IERC20(weth).safeTransferFrom(sender, wrapper, amount);
            amount = _wethToWsteth(amount);
        } else if (token == address(0)) {
            if (msg.value != amount) revert InvalidAmount();
            amount = _ethToWsteth(amount);
        } else if (wsteth == token) {
            IERC20(wsteth).safeTransferFrom(sender, wrapper, amount);
        } else revert InvalidToken();

        IERC20(wsteth).safeIncreaseAllowance(address(vault), amount);
        uint256[] memory amounts = new uint256[](1);
        amounts[0] = amount;
        (, lpAmount) = vault.deposit(to, amounts, minLpAmount, deadline);
        uint256 balance = IERC20(wsteth).balanceOf(wrapper);
        if (balance > 0) IERC20(wsteth).safeTransfer(sender, balance);
        emit DepositWrapperDeposit(sender, token, amount, lpAmount, deadline);
    }
```
```solidity
File: DepositWrapper.sol
   function _stethToWsteth(uint256 amount) private returns (uint256) {
        IERC20(steth).safeIncreaseAllowance(wsteth, amount);
        IWSteth(wsteth).wrap(amount);
        return IERC20(wsteth).balanceOf(address(this));
    }
```

## Impact
Denial of service on the Deposit function for cases when `steth.safeTransferFrom(...)` transfers lesser tokens as stated here in the [docs](https://docs.lido.fi/guides/lido-tokens-integration-guide#1-2-wei-corner-case)


## Code Snippet
- https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/utils/DepositWrapper.sol#L55C9-L57C45
- https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/utils/DepositWrapper.sol#L35C5-L39C6


## Tool used
Manual Review

## Reference
Here is a similar issue in a past contest: https://solodit.xyz/issues/h-2-many-cases-stethtransferfrom-will-transfer-1-2-less-way-which-would-result-in-revert-in-consequent-functions-because-of-not-enough-balance-sherlock-sophon-farming-contracts-git

## Recommendation
Consider using the `transferShares(...)` or `transferShareFrom(...)` function of the stEth contract to transfer stEth as recommended in the [docs](https://docs.lido.fi/guides/lido-tokens-integration-guide#transfer-shares-function-for-steth)

