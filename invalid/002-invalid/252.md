Shaggy Holographic Tuna

High

# ``ISteth(steth).submit`` function doesn't return the exact amount of stETH as input ETH amount which can cause reverting of consequent functions.

## Summary
``ISteth(steth).submit`` function doesn't return the exact amount of stETH as input ETH amount which can cause reverting of consequent functions.

## Vulnerability Detail
In the ``DepositWrapper`` contract, we can deposit ``ETH`` and it is then converted to ``stETH`` and then to ``wstETH``. 
First ETH is converted to ``stETH``:
```solidity
    function deposit(
        address to,
        address token,
        uint256 amount,
        uint256 minLpAmount,
        uint256 deadline
    ) external payable returns (uint256 lpAmount) {
        address wrapper = address(this);
        address sender = msg.sender;
        address[] memory tokens = vault.underlyingTokens();
        if (tokens.length != 1 || tokens[0] != wsteth)
            revert InvalidTokenList();
        if (amount == 0) revert InvalidAmount();
        if (token == steth) {
            IERC20(steth).safeTransferFrom(sender, wrapper, amount);
            amount = _stethToWsteth(amount);
        } else if (token == weth) {
            IERC20(weth).safeTransferFrom(sender, wrapper, amount);
            amount = _wethToWsteth(amount);
        } else if (token == address(0)) {
            if (msg.value != amount) revert InvalidAmount();
            amount = _ethToWsteth(amount);
...
```
```solidity
    function _ethToWsteth(uint256 amount) private returns (uint256) {
        ISteth(steth).submit{value: amount}(address(0));
        return _stethToWsteth(amount);
    }
```
It uses ``ISteth(steth).submit{value: amount}(address(0))`` to convert ``ETH`` to ``stETH`` and same amount is used to convert from ``stETH`` to ``wstETH``. The problem is if submit doesn't return same ``amount`` of ``ETH`` deposited, the conversion of ``stETH`` to ``wstETH``will revert due to insufficient funds and ``DepositWrapper`` contract doesn't hold any funds.
```solidity
    function _stethToWsteth(uint256 amount) private returns (uint256) {
        IERC20(steth).safeIncreaseAllowance(wsteth, amount);
        IWSteth(wsteth).wrap(amount);
        return IERC20(wsteth).balanceOf(address(this));
    }
```
> This same issue also exists in ``StakingModule`` contract too and any contract/function using ``convertAndDeposit()`` of ``StakingModule`` contract can also revert.
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/modules/obol/StakingModule.sol#L79

## Impact
Contract Functionality DoS.

## Code Snippet
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/utils/DepositWrapper.sol#L31
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/modules/obol/StakingModule.sol#L79

## Tool used
Manual Review

## Recommendation
Modify the ``_ethToWsteth`` of ``DepositWrapper`` as below:
```solidity
    function _ethToWsteth(uint256 amount) private returns (uint256) {
        uint256 balanceBefore = IERC20(steth).balanceOf(address(this));
        ISteth(steth).submit{value: amount}(address(0));
        return _stethToWsteth(IERC20(steth).balanceOf(address(this)) - balanceBefore);
    }
```

