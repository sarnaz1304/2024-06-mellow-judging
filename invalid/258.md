Tiny Spruce Cheetah

Medium

# Some users can't emergency withdraw their LRTs.

## Summary

In emergency status, all users should be able to withdraw their LRTs.
But in a certain case, some users can't emergency withdraw their LRTs.

## Vulnerability Detail

At first, let us look at the following code.
```solidity
File: Vault.sol
    function emergencyWithdraw(
        uint256[] memory minAmounts,
        uint256 deadline
    )
        external
        nonReentrant
        checkDeadline(deadline)
        returns (uint256[] memory actualAmounts)
    {
        ......
        uint256 totalSupply = totalSupply();
395:    (address[] memory tokens, uint256[] memory amounts) = baseTvl();
        if (minAmounts.length != tokens.length) revert InvalidLength();
        actualAmounts = new uint256[](tokens.length);
        for (uint256 i = 0; i < tokens.length; i++) {
            if (amounts[i] == 0) {
400:            if (minAmounts[i] != 0) revert InsufficientAmount();
                continue;
            }
            uint256 amount = FullMath.mulDiv(
404:            IERC20(tokens[i]).balanceOf(address(this)),
                request.lpAmount,
                totalSupply
            );
            if (amount < minAmounts[i]) revert InsufficientAmount();
            IERC20(tokens[i]).safeTransfer(request.to, amount);
            actualAmounts[i] = amount;
        }
        ......
    }
```
if `amounts[i] == 0` and `minAmounts[i] > 0` in `L400`, the user can't emergency withdraw their LRTs.
Here `amounts[i]` is the total sum of TVLs of all TVL modules for the token `tokens[i]`.
However, there can be the case that `amounts[i]` is smaller than `tokens[i].balanceOf(this)` of `L404`.

Scenario:
1. Now suppose that `tokens = [token1]`, `amounts = [200]` in `L395` and `tokens[0].balanceOf(this) == 600` in `L404`.
For instance, this is the case that there are two TVL modules, such that the base TVL of the first TVL module `module1`(=`ERC20TvlModule`) is `(token1, 600)` and the base TVL of the second one `module2` is `(token1, -400)`.
2. Suppose that there are only two users in the vault whose LP amount is `100`. Therefore the `totalSupply` of the vault is `200`.
3. At first, the `user1` calls `emergencyWithdraw` function with `lpAmount=100`. Then, since `amount = 600 * 100 / 200 == 300` in `L404`, `user1` can withdraw `300` of `token1` from the vault.
4. At that time, the base TVL of `module1` will be decreased to `(token1, 300)` and so the total sum of base TVLs for `token1` is `0 = max(0, 300 - 400)` (see implementation of `baseTvl()` function for more detail).
5. Next, the `user2` calls `emergencyWithdraw` with `lpAmount=100`. Then, since the base TVL of `L395` will be `(token1, 0)`, the `user2` cannot withdraw any amount of `token1`. 

## Impact

In emergency status, all users should be able to withdraw their LRTs.
However, as shown above, if there exists a TVL module with debt TVL and the balance of vault is greater than the total sum of base TVLs of all TVL modules, some late users can't withdraw their LRTs at all.

## Code Snippet

https://github.com/sherlock-audit/2024-06-mellow/tree/main/mellow-lrt/src/Vault.sol#L372-L416

## Tool used

Manual Review

## Recommendation

There are two solutions to this issue: in favor of users or in favor of protocol.
First solution in favor of users:
```diff
    function emergencyWithdraw(
        uint256[] memory minAmounts,
        uint256 deadline
    )
        external
        nonReentrant
        checkDeadline(deadline)
        returns (uint256[] memory actualAmounts)
    {
        ......
        uint256 totalSupply = totalSupply();
        (address[] memory tokens, uint256[] memory amounts) = baseTvl();
        if (minAmounts.length != tokens.length) revert InvalidLength();
        actualAmounts = new uint256[](tokens.length);
        for (uint256 i = 0; i < tokens.length; i++) {
+           // withdraw all available tokens in the vault to users, ignoring debts
-           if (amounts[i] == 0) {
-               if (minAmounts[i] != 0) revert InsufficientAmount();
-               continue;
-           }
            uint256 amount = FullMath.mulDiv(
                IERC20(tokens[i]).balanceOf(address(this)),
                request.lpAmount,
                totalSupply
            );
            if (amount < minAmounts[i]) revert InsufficientAmount();
            IERC20(tokens[i]).safeTransfer(request.to, amount);
            actualAmounts[i] = amount;
        }
        ......
    }
```
Second solution in favor of the protocol:
```diff
    function emergencyWithdraw(
        uint256[] memory minAmounts,
        uint256 deadline
    )
        external
        nonReentrant
        checkDeadline(deadline)
        returns (uint256[] memory actualAmounts)
    {
        ......
        uint256 totalSupply = totalSupply();
        (address[] memory tokens, uint256[] memory amounts) = baseTvl();
        if (minAmounts.length != tokens.length) revert InvalidLength();
        actualAmounts = new uint256[](tokens.length);
        for (uint256 i = 0; i < tokens.length; i++) {
-           if (amounts[i] == 0) {
-               if (minAmounts[i] != 0) revert InsufficientAmount();
-               continue;
-           }
+           // get minimum of amounts[i] and tokens[i].balanceOf(this)
+           uint256 minAmount = IERC20(tokens[i]).balanceOf(address(this));
+           if (minAmount > amounts[i]) minAmount = amounts[i];
            uint256 amount = FullMath.mulDiv(
-               IERC20(tokens[i]).balanceOf(address(this)),
+               minAmount,
                request.lpAmount,
                totalSupply
            );
            if (amount < minAmounts[i]) revert InsufficientAmount();
            IERC20(tokens[i]).safeTransfer(request.to, amount);
            actualAmounts[i] = amount;
        }
        ......
    }
```