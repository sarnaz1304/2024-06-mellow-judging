Droll Ash Nuthatch

High

# Vault::analyzeRequest calculates vault state only once and will fail to execute withdrawals

## Summary

Using the same exchange rate to calculate all the user `expectedAmounts` will end up not being able to satisfy all the withdrawals.

## Vulnerability Detail

Currently, the vault stack is calculated only once before all the withdrawal requests are iterated and fulfilled:

```solidity
  function processWithdrawals(
      address[] memory users
  ) external nonReentrant returns (bool[] memory statuses) {
      _requireAtLeastOperator();
      statuses = new bool[](users.length);
      ProcessWithdrawalsStack memory s = calculateStack();//@audit here
      uint256 burningSupply = 0;
      for (uint256 i = 0; i < users.length; i++) {
          address user = users[i];
          if (!_pendingWithdrawers.contains(user)) continue;
          WithdrawalRequest memory request = _withdrawalRequest[user];
          (
              bool isProcessingPossible,
              bool isWithdrawalPossible,
              uint256[] memory expectedAmounts
          ) = analyzeRequest(s, request);

          if (!isProcessingPossible) {
              _cancelWithdrawalRequest(user);
              continue;
          }

          if (!isWithdrawalPossible) continue;

          for (uint256 j = 0; j < s.tokens.length; j++) {
              s.erc20Balances[j] -= expectedAmounts[j];
              IERC20(s.tokens[j]).safeTransfer(
                  request.to,
                  expectedAmounts[j]
              );
          }
          burningSupply += request.lpAmount;
          delete _withdrawalRequest[user];
          _pendingWithdrawers.remove(user);
          statuses[i] = true;
      }

      if (burningSupply == 0) return statuses;
      _burn(address(this), burningSupply);
      emit WithdrawalsProcessed(users, statuses);

      address callback = configurator.withdrawalCallback();
      if (callback == address(0)) return statuses;
      IWithdrawalCallback(callback).withdrawalCallback();
      emit WithdrawCallback(callback);
}
```

and then the same `ProcessWithdrawalsStack` untouched. But in the scenarios when the exchange rate of lpTokens is not 1:1 but above 1, not all withdrawals will be fulfilled. 

Consider the following example:

1. underlying assets =  `wsETH, rETH`
2. balances = 1 from each
3. ratios = 1:1

Then `Vault::calculateStack`:

```solidity
struct ProcessWithdrawalsStack {
        address[] tokens;// wsETH, rETH
        uint128[] ratiosX96;// 1:3
        uint256[] erc20Balances;//1 wstETH, 1 rETH
        uint256 totalSupply;//2 mETH
        uint256 totalValue;// 2.4 ETH 
        uint256 ratiosX96Value;//46378103694101574704724723335, 46378103694101574704724723335 = 92756207388203149409449446670
        uint256 timestamp;
        uint256 feeD9; // 0
        bytes32 tokensHash; 
    }
```

Current exchange rate = 2 mETH : 2.4 ETH

2 users, each of them requested 1 mETH token:

```solidity
 function analyzeRequest(
        ProcessWithdrawalsStack memory s,
        WithdrawalRequest memory request
    ) public pure returns (bool, bool, uint256[] memory expectedAmounts) {
        uint256 lpAmount = request.lpAmount;
        uint256 value = FullMath.mulDiv(lpAmount, s.totalValue, s.totalSupply);
        value = FullMath.mulDiv(value, D9 - s.feeD9, D9);
        uint256 coefficientX96 = FullMath.mulDiv(value, Q96, s.ratiosX96Value);

        uint256 length = s.erc20Balances.length;
        expectedAmounts = new uint256[](length);
        for (uint256 i = 0; i < length; i++) {
            uint256 ratiosX96 = s.ratiosX96[i];
            expectedAmounts[i] = ratiosX96 == 0
                ? 0
                : FullMath.mulDiv(coefficientX96, ratiosX96, Q96);
            if (expectedAmounts[i] >= request.minAmounts[i]) continue;
            return (false, false, expectedAmounts);
        }
        for (uint256 i = 0; i < length; i++) {
            if (s.erc20Balances[i] >= expectedAmounts[i]) continue;
            return (true, false, expectedAmounts);
        }
        return (true, true, expectedAmounts);
    }
```

On first withdrawal:

1. value  = 1e18 * 2.4e18 / 2e18 = 1.2e18
2. coefficientX96 = (1.2e18 * 2^96) / 92756207388203149409449446670 = 1024985795497378317 (1.02) 
    1. epectedAmount[0] = (1024985795497378317 * (2^96 / 2)) / 2^96 = 512492897748689158 (0.5124)
    2. epectedAmount[1] = (1024985795497378317 * (2^96 / 2)) / 2^96 = 512492897748689158 (0.5124) 

And after withdrawal:

```solidity
struct ProcessWithdrawalsStack {
        address[] tokens;// wsETH, rETH
        uint128[] ratiosX96;// 1:1
        uint256[] erc20Balances;//487507102251310842 wstETH, 487507102251310842 rETH
        uint256 totalSupply;//1 mETH
        uint256 totalValue;// 975014204502621684 ETH 
        uint256 ratiosX96Value;//46378103694101574704724723335, 46378103694101574704724723335 = 92756207388203149409449446670
        uint256 timestamp;
        uint256 feeD9; // 0
        bytes32 tokensHash; 
    }
```

Since second user also requested 1 mETH token, so his `expectedAmounts` will be the same, meaning that he will need 0.5124 of each of the underlying tokens, but in the vault there are only 0.4875 of each underlying token and the transfer in `processWithdrawals` will revert with insufficient balance:

```solidity
IERC20(s.tokens[j]).safeTransfer(
                request.to,
                expectedAmounts[j]
            );
```

## Impact

Exchange rate is not calculated for each request individually, preventing some withdrawals to happen. 

## Code Snippet

https://github.com/mellow-finance/mellow-lrt/blob/dev-symbiotic-deploy/src/Vault.sol#L536

## Tool used

Manual Review

## Recommendation

Consider recalculating the vault state each time, accounting for both transferred underlying tokens and burned lpTokens.