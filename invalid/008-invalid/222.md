Droll Ash Nuthatch

Medium

# Off-by-one in ChainlinkOracle will deliver wrong prices when answer is 0

## Summary

In case of Oracle malfunction and returning 0 as `answer`, the system will revert.

## Vulnerability Detail

`_validateAndGetPrice()` only checks if the price reported by the price feed is negative, but not if the price **is = 0**. Execution will continue and possibly revurt with division by 0 when calculating `priceX96`.

```solidity
 function _validateAndGetPrice(
    AggregatorData memory data
) private view returns (uint256 answer, uint8 decimals) {
    if (data.aggregatorV3 == address(0)) revert AddressZero();
    (, int256 signedAnswer, , uint256 lastTimestamp, ) = IAggregatorV3( 
        data.aggregatorV3
    ).latestRoundData();
    // The roundId and latestRound are not used in the validation process to ensure compatibility
    // with various custom aggregator implementations that may handle these parameters differently
    if (signedAnswer < 0) revert InvalidOracleData();
    answer = uint256(signedAnswer);
    if (block.timestamp - data.maxAge > lastTimestamp) revert StaleOracle();
    decimals = IAggregatorV3(data.aggregatorV3).decimals();
}
```

```solidity
function priceX96(
    address vault,
    address token
) external view returns (uint256 priceX96_) {
    if (vault == address(0)) revert AddressZero();
    if (token == address(0)) revert AddressZero();
    address baseToken = baseTokens[vault];
    if (baseToken == address(0)) revert AddressZero();
    if (token == baseToken) return Q96;
    (uint256 tokenPrice, uint8 decimals) = getPrice(vault, token); // tokenPrice can be = 0
    (uint256 baseTokenPrice, uint8 baseDecimals) = getPrice(
        vault,
        baseToken
    );
    priceX96_ = FullMath.mulDiv(
        tokenPrice * 10 ** baseDecimals, 
        Q96,
        baseTokenPrice * 10 ** decimals 
    );
}
```

## Impact

All key protocol functions will malfunction due to division by zero and 0 price reported from the oracle.

## Code Snippet
https://github.com/mellow-finance/mellow-lrt/blob/8191087682cc6a7f36c1c6390e37eb308953b11a/src/oracles/ChainlinkOracle.sol#L65

## Tool used

Manual Review

## Recommendation

Change the require so that it does not allow 0 price.

```diff
 function _validateAndGetPrice(
    AggregatorData memory data
) private view returns (uint256 answer, uint8 decimals) {
    if (data.aggregatorV3 == address(0)) revert AddressZero();
    (, int256 signedAnswer, , uint256 lastTimestamp, ) = IAggregatorV3( 
        data.aggregatorV3
    ).latestRoundData();
    // The roundId and latestRound are not used in the validation process to ensure compatibility
    // with various custom aggregator implementations that may handle these parameters differently
-   if (signedAnswer < 0) revert InvalidOracleData();
+   if (signedAnswer <= 0) revert InvalidOracleData();
    answer = uint256(signedAnswer);
    if (block.timestamp - data.maxAge > lastTimestamp) revert StaleOracle();
    decimals = IAggregatorV3(data.aggregatorV3).decimals();
}
```