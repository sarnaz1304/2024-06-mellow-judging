Droll Ash Nuthatch

Medium

# _calculateTvl do not have limit for the tokens and modules, potentially running out of gas

## Summary

`_calculateTvl` has no token limit and it will run out of gas because all functions call `underlyingTvl()` which has 6 `for` loops inside it.

## Vulnerability Detail

Every Vault operation (`deposit`, `withdraw`, `removeToken`) first gets the Tvl by calling `underlyingTvl()`. Inside `underlyingTvl`, `_calculateTvl` and `_tvls` are 6 `for` loops or as with the `emergencyWithdraw` when calling `baseTvl` there are 10 `for` loops. All these numbers are just for fetching `tvl`, saying `deposit`, `withdraw` and `removeToken` have 1-2 `for` loops inside them for the calculations, resulting in **10+ `for` loops**. And if modules and tokens increase, it will be very possible that any of these functions will run out of gas.

[[Vault.sol#L86-L171](https://github.com/sherlock-audit/2024-06-mellow/blob/26aa0445ec405a4ad637bddeeedec4efe1eba8d2/mellow-lrt/src/Vault.sol#L86-L171)](https://github.com/sherlock-audit/2024-06-mellow/blob/26aa0445ec405a4ad637bddeeedec4efe1eba8d2/mellow-lrt/src/Vault.sol#L86-L171)

```solidity
function underlyingTvl()
    public
    view
    returns (address[] memory tokens, uint256[] memory amounts)
{
    tokens = _underlyingTokens;
    amounts = _calculateTvl(tokens, true);
}

/// @inheritdoc IVault
function baseTvl()
    public
    view
    returns (address[] memory tokens, uint256[] memory amounts)
{
    ITvlModule.Data[] memory data = _tvls();
    tokens = new address[](data.length);
    uint256 length = 0;
    for (uint256 i = 0; i < data.length; i++) {
        if (data[i].token == address(0)) continue;
        uint256 tokenIndex = length;
        for (uint256 j = 0; j < length; j++) {
            if (tokens[j] != data[i].token) continue;
            tokenIndex = j;
            break;
        }
        if (tokenIndex != length) continue;
        tokens[tokenIndex] = data[i].token;
        length++;
    }
    for (uint256 i = 0; i < length; i++) {
        for (uint256 j = i + 1; j < length; j++) {
            if (tokens[i] < tokens[j]) continue;
            (tokens[i], tokens[j]) = (tokens[j], tokens[i]);
        }
    }
    assembly {
        mstore(tokens, length)
    }
    amounts = _calculateTvl(tokens, false);
}

function _calculateTvl(
    address[] memory tokens,
    bool isUnderlying
) private view returns (uint256[] memory amounts) {
    amounts = new uint256[](tokens.length);
    uint256[] memory negativeAmounts = new uint256[](tokens.length);
    ITvlModule.Data[] memory tvl_ = _tvls();
    ITvlModule.Data memory data;
    for (uint256 i = 0; i < tvl_.length; i++) {
        data = tvl_[i];
        (uint256 amount, address token) = isUnderlying
            ? (data.underlyingAmount, data.underlyingToken)
            : (data.amount, data.token);
        for (uint256 j = 0; j < tokens.length; j++) {
            if (token != tokens[j]) continue;
            (data.isDebt ? negativeAmounts : amounts)[j] += amount;
            break;
        }
    }
    for (uint256 i = 0; i < tokens.length; i++) {
        if (amounts[i] < negativeAmounts[i]) revert InvalidState();
        amounts[i] -= negativeAmounts[i];
    }
}

function _tvls() private view returns (ITvlModule.Data[] memory data) {
    ITvlModule.Data[][] memory responses = new ITvlModule.Data[][](
        _tvlModules.length()
    );
    uint256 length = 0;
    for (uint256 i = 0; i < responses.length; i++) {
        address module = _tvlModules.at(i);
        responses[i] = ITvlModule(module).tvl(address(this));
        length += responses[i].length;
    }
    data = new ITvlModule.Data[](length);
    uint256 index = 0;
    for (uint256 i = 0; i < responses.length; i++) {
        for (uint256 j = 0; j < responses[i].length; j++) {
            data[index++] = responses[i][j];
        }
    }
}
```

## Impact

Since the tokens and modules do not have upper limit, when they grow the external functions in the contract may run out of gas.

## Code Snippet

https://github.com/sherlock-audit/2024-06-mellow/blob/26aa0445ec405a4ad637bddeeedec4efe1eba8d2/mellow-lrt/src/Vault.sol#L86-L171

## Tool used

Manual Review

The exact same issue was recently reported here - 

https://github.com/code-423n4/2024-04-renzo-findings/issues/514

## Recommendation

Consider having max number for tokens and modules.