Ancient Pastel Beaver

High

# It is impossible to call `Vault::delegateCall` through external methods as it is intended

## Summary
`Vault::delegateCall` is called through `SimpleDVTStakingStrategy::convertAndDeposit`, `SimpleDVTStakingStrategy::processWithdrawals`, `DefaultBondStrategy::_deposit` and `DefaultBondStrategy::_processWithdrawals` but the calls will always revert due to permission checks.

## Vulnerability Detail

When `Vault::DelegateCall` is called it checks weather the caller is an Operator/Admin and if not it reverts

```solidity
    /// @inheritdoc IVault
    function delegateCall(
        address to,
        bytes calldata data
    ) external returns (bool success, bytes memory response) {
@>      _requireAtLeastOperator();
        if (!configurator.isDelegateModuleApproved(to)) revert Forbidden();
        IValidator validator = IValidator(configurator.validator());
        validator.validate(
            msg.sender,
            address(this),
            abi.encodeWithSelector(msg.sig, to, data)
        );
        validator.validate(address(this), to, data);
        (success, response) = to.delegatecall(data);
        emit DelegateCall(to, data, success, response);
    }
```

But all contracts use a normal call to the function and not a delegatecall, meaning that `msg.sender` is always going to be the contract's address and not an address of an Operator/Admin meaning that the transaction will always revert.

```solidity
   /// @inheritdoc ISimpleDVTStakingStrategy
    function convertAndDeposit(
        uint256 amount,
        uint256 blockNumber,
        bytes32 blockHash,
        bytes32 depositRoot,
        uint256 nonce,
        bytes calldata depositCalldata,
        IDepositSecurityModule.Signature[] calldata sortedGuardianSignatures
    ) external returns (bool success) {
 @>     (success, ) = vault.delegateCall(
            address(stakingModule),
            abi.encodeWithSelector(
                IStakingModule.convertAndDeposit.selector,
                amount,
                blockNumber,
                blockHash,
                depositRoot,
                nonce,
                depositCalldata,
                sortedGuardianSignatures
            )
        );
        emit ConvertAndDeposit(success, msg.sender);
    }
```

```solidity
    /// @inheritdoc ISimpleDVTStakingStrategy
    function processWithdrawals(
        address[] memory users,
        uint256 amountForStake
    ) external returns (bool[] memory statuses) {
        _requireAtLeastOperator();
        if (users.length == 0) return statuses;
        emit ProcessWithdrawals(users, amountForStake, msg.sender);

        if (amountForStake == 0) return vault.processWithdrawals(users);

@>        vault.delegateCall(
            address(stakingModule),
            abi.encodeWithSelector(
                IStakingModule.convert.selector,
                amountForStake
            )
        );

        statuses = vault.processWithdrawals(users);
        address wsteth = stakingModule.wsteth();
        uint256 balance = IERC20(wsteth).balanceOf(address(vault));
        if (balance > maxAllowedRemainder) revert LimitOverflow();
    }
```

```solidity
    function _deposit() private {
        ITvlModule.Data[] memory tvl = erc20TvlModule.tvl(address(vault));
        for (uint256 i = 0; i < tvl.length; i++) {
            address token = tvl[i].token;
            bytes memory data_ = tokenToData[token];
            if (data_.length == 0) continue;
            Data[] memory data = abi.decode(data_, (Data[]));
            for (uint256 j = 0; j < data.length; j++) {
                uint256 amount = FullMath.mulDiv(
                    tvl[i].amount,
                    data[j].ratioX96,
                    Q96
                );
                if (amount == 0) continue;
@>              vault.delegateCall(
                    address(bondModule),
                    abi.encodeWithSelector(
                        IDefaultBondModule.deposit.selector,
                        data[j].bond,
                        amount
                    )
                );
            }
        }
    }
```

```solidity
    function _processWithdrawals(address[] memory users) private {
        if (users.length == 0) return;

        address[] memory tokens = vault.underlyingTokens();
        for (uint256 index = 0; index < tokens.length; index++) {
            bytes memory data_ = tokenToData[tokens[index]];
            if (data_.length == 0) continue;
            Data[] memory data = abi.decode(data_, (Data[]));
            for (uint256 i = 0; i < data.length; i++) {
                uint256 amount = IERC20(data[i].bond).balanceOf(address(vault));
                if (amount == 0) continue;
@>              vault.delegateCall(
                    address(bondModule),
                    abi.encodeWithSelector(
                        IDefaultBondModule.withdraw.selector,
                        data[i].bond,
                        amount
                    )
                );
            }
        }

        vault.processWithdrawals(users);
        _deposit();
        emit DefaultBondStrategyProcessWithdrawals(users, block.timestamp);
    }
```
## Impact
These functions become unusable and force the Operators/Admins to only make deleagate calls by directly calling `Vault::delegateCall` instead of using the already created methods that make their job much easier and mistake proof.
## Code Snippet

https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/Vault.sol#L266-L282
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/strategies/SimpleDVTStakingStrategy.sol#L36-L85
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/strategies/DefaultBondStrategy.sol#L51-L75
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/strategies/DefaultBondStrategy.sol#L96-L121
## Tool used

Manual Review

## Recommendation

Instead of first checking if the msg.sender is an Operator or Admin, check if the call is coming from one of the strategy contracts and it if is, bypass the check for Operator/Admin