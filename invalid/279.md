Ancient Pastel Beaver

Medium

# `Vault::deposit()` will always revert when `configurator.depositCallback()` is set to `DefaultBondStrategy::depositCallback()`

## Summary

When a users calls `Vault::deposit()` the contract has the ability to set a deposit callback function which is intended to be `DefaultBondStrategy::depositCallback`. The problem is that because of how to calls happen the transaction will eventually revert because of lack of permission.

## Vulnerability Detail

When a user calls `Vault::deposit()` the function will then call `DefaultBondStrategy::depositCallback` which is going to make the permission check and because `msg.sender` will be the vault contract the next function called will be `DefaultBondStrategy::_deposit`

```solidity
    /// @inheritdoc IDepositCallback
    function depositCallback(uint256[] memory, uint256) external override {
        if (msg.sender != address(vault)) _requireAtLeastOperator();
        _deposit();
    }
```

There happens the next call which is calling back to the vault contract.

```solidity
    function _deposit() private {
        ITvlModule.Data[] memory tvl = erc20TvlModule.tvl(address(vault));
        for (uint256 i = 0; i < tvl.length; i++) {
            address token = tvl[i].token;
            bytes memory data_ = tokenToData[token];
            if (data_.length == 0) continue;
            Data[] memory data = abi.decode(data_, (Data[]));
            for (uint256 j = 0; j < data.length; j++) {
                uint256 amount = FullMath.mulDiv(
                    tvl[i].amount,
                    data[j].ratioX96,
                    Q96
                );
                if (amount == 0) continue;
@>              vault.delegateCall(
                    address(bondModule),
                    abi.encodeWithSelector(
                        IDefaultBondModule.deposit.selector,
                        data[j].bond,
                        amount
                    )
                );
            }
        }
    }
```

Then if we take a look inside `Vault::delegateCall` we are going to notice the `_requireAtleastOperator()` function being called which is going to make the whole transaction revert because `msg.sender` is going to be the `DefaultBondStrategy` contract, meaning that users will not be able to deposit any funds if the deposit callback function is set to `DefaultBondStrategy::depositCallback()`

```solidity
    /// @inheritdoc IVault
    function delegateCall(
        address to,
        bytes calldata data
    ) external returns (bool success, bytes memory response) {
@>        _requireAtLeastOperator();
...
```
```solidity
    function _requireAtLeastOperator(address sender) internal view {
        if (!isAdmin(sender) && !isOperator(sender)) revert Forbidden();
    }
```
## Impact

If the deposit callback function is set then `Vault::deposit()` will be locked and users will not be able to deposit.
If the deposit callback function is not set then the funds will not be automatically deposited into bonds and will sit inside the vault contract and an Operator/Admin will have to manually call `DefaultBondStrategy::depositCallback()` to deposit the funds into bonds.
## Code Snippet

https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/Vault.sol#L340-L342
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/strategies/DefaultBondStrategy.sol#L77-L81
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/strategies/DefaultBondStrategy.sol#L51-L75
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/Vault.sol#L266-L282

## Tool used

Manual Review

## Recommendation
Put a similar check inside `Vault::delegateCall`

```diff
    /// @inheritdoc IVault
    function delegateCall(
        address to,
        bytes calldata data
    ) external returns (bool success, bytes memory response) {        
-  _requireAtLeastOperator();
+   if (msg.sender != address(defaultBondsModule)) _requireAtLeastOperator();
...
```
