Droll Ash Nuthatch

Medium

# StakingModule::convertAndDeposit wrong buffered and unfinalized check can prevent some deposits

## Summary

The unnecessary check performed in `StakingModule::convertAndDeposit` will prevent new deposits in Lido when `WithdrawalQueue::unfinalizedStETH` is bigger than `Lido::getBufferedEther`.

## Vulnerability Detail

In order to understand the issue with this superfluous check we should take a look at `stETH::submit`:

```solidity
function _submit(address _referral) internal returns (uint256) {
    require(msg.value != 0, "ZERO_DEPOSIT");

    StakeLimitState.Data memory stakeLimitData = STAKING_STATE_POSITION.getStorageStakeLimitStruct();
    // There is an invariant that protocol pause also implies staking pause.
    // Thus, no need to check protocol pause explicitly.
    require(!stakeLimitData.isStakingPaused(), "STAKING_PAUSED");

    if (stakeLimitData.isStakingLimitSet()) {
        uint256 currentStakeLimit = stakeLimitData.calculateCurrentStakeLimit();

        require(msg.value <= currentStakeLimit, "STAKE_LIMIT");

        STAKING_STATE_POSITION.setStorageStakeLimitStruct(stakeLimitData.updatePrevStakeLimit(currentStakeLimit - msg.value));
    }

    uint256 sharesAmount = getSharesByPooledEth(msg.value);

    _mintShares(msg.sender, sharesAmount);

    _setBufferedEther(_getBufferedEther().add(msg.value));//@audit this updates the getBufferedEther
    emit Submitted(msg.sender, msg.value, _referral);

    _emitTransferAfterMintingShares(msg.sender, sharesAmount);
    return sharesAmount;
}
```

As we can see it increases the `bufferedEther` when new `ETH` is being staked.

Indeed the exact same check is performed inside `depositSecurityModule::depositBufferedEther`, as it ends up calling the following functions from `stETH`:

```solidity
function deposit(uint256 _maxDepositsCount, uint256 _stakingModuleId, bytes _depositCalldata) external {
      ILidoLocator locator = getLidoLocator();

      require(msg.sender == locator.depositSecurityModule(), "APP_AUTH_DSM_FAILED");
      require(canDeposit(), "CAN_NOT_DEPOSIT");

      IStakingRouter stakingRouter = _stakingRouter();
      uint256 depositsCount = Math256.min(
          _maxDepositsCount,
          stakingRouter.getStakingModuleMaxDepositsCount(_stakingModuleId, getDepositableEther())//@audit check hereb 
      );
    ...MORE CODE
  }
```

```solidity
/**
     * @dev Returns depositable ether amount.
     * Takes into account unfinalized stETH required by WithdrawalQueue
     */
    function getDepositableEther() public view returns (uint256) {
        uint256 bufferedEther = _getBufferedEther();
        uint256 withdrawalReserve = _withdrawalQueue().unfinalizedStETH();
        return bufferedEther > withdrawalReserve ? bufferedEther - withdrawalReserve : 0;
    }
```

Now, knowing that `stETH::submit` increases `bufferedEther` letâ€™s consider the following scenario:

- `bufferedEther` = 95 ETH
- `unfinalizedStETH` = 100 ETH
- `StakingModule::convertAndDeposit` with 10 `ETH`:

```solidity
function convertAndDeposit(
  uint256 amount,
  uint256 blockNumber,
  bytes32 blockHash,
  bytes32 depositRoot,
  uint256 nonce,
  bytes calldata depositCalldata,
  IDepositSecurityModule.Signature[] calldata sortedGuardianSignatures
) external onlyDelegateCall {
  if (IERC20(weth).balanceOf(address(this)) < amount)
      revert NotEnoughWeth();

  uint256 unfinalizedStETH = withdrawalQueue.unfinalizedStETH();
  uint256 bufferedEther = ISteth(steth).getBufferedEther();
  if (bufferedEther < unfinalizedStETH)
      revert InvalidWithdrawalQueueState();

  _wethToWSteth(amount);
  depositSecurityModule.depositBufferedEther(
      blockNumber,
      blockHash,
      depositRoot,
      stakingModuleId,
      nonce,
      depositCalldata,
      sortedGuardianSignatures
  );
}

function _wethToWSteth(uint256 amount) private {
  IWeth(weth).withdraw(amount);
  ISteth(steth).submit{value: amount}(address(0));
  IERC20(steth).safeIncreaseAllowance(address(wsteth), amount);
  IWSteth(wsteth).wrap(amount);
}
```

In this configuration all Lido functions should have passed successfully, since first `ETH` is submitted and then `depositBufferedEther` is called that will use the most recent `bufferedEther` which will become 105 `ETH` and 100 `unfinalizedStETH`. 

Instead the tx will revert unfairly preventing Vault from depositing into Lido, as the `bufferedEther` will be before staking and will remain at 95 and the `unfinalizedStETH` = 100.

## Impact

- superfluous checks will prevent `StakingModule` from depositing into Lido

## Code Snippet

https://github.com/mellow-finance/mellow-lrt/blob/dev-symbiotic-deploy/src/modules/obol/StakingModule.sol#L48-L82

## Tool used

Manual Review

## Recommendation

Remove the check completely or perform it after `_wethToWSteth` is executed and the `bufferedEther` is being updated