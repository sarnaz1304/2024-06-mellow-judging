Old Khaki Kitten

Medium

# [M-01] `ChainlinkOracle::_validateAndGetPrice` Accepts zero as a valid Price Due to Wrong Condition

## Summary
The `_validateAndGetPrice` private function gets the price from the AggregatorV3 and then checks if the signedAnswer is less than zero to revert if this case is true, but it accepts the `zero` as a correct prics and returns it as current price.
```javascript
    function _validateAndGetPrice(
        AggregatorData memory data
    ) private view returns (uint256 answer, uint8 decimals) {
        if (data.aggregatorV3 == address(0)) revert AddressZero();
@>      (, int256 signedAnswer, , uint256 lastTimestamp, ) = IAggregatorV3(data.aggregatorV3).latestRoundData();
        // The roundId and latestRound are not used in the validation process to ensure compatibility
        // with various custom aggregator implementations that may handle these parameters differently
@>      if (signedAnswer < 0) revert InvalidOracleData();
@>      answer = uint256(signedAnswer);
        if (block.timestamp - data.maxAge > lastTimestamp) revert StaleOracle();
        decimals = IAggregatorV3(data.aggregatorV3).decimals();
    }
```
## Vulnerability Detail
In case there is a stall price or a problem with the chainlink aggregator contract the price might return zero and if this is the case the `_validateAndGetPrice` should revert instead of returning zero.
## Impact
if someone manages to bring the chainlink oracle down, all the values for vault tvl would be wrong and tvl would be zero. 
The test bellow shows that price of zero indeed passes.
```javascript
    function testZeroSignedAnswer() external {
        address admin = address(bytes20(keccak256("vault-admin")));
        ChainlinkOracle oracle = new ChainlinkOracle();
        VaultMock vault = new VaultMock(admin);
        address[] memory tokens = new address[](1);
        tokens[0] = Constants.RETH;

        address[] memory oracles = new address[](1);
        AggregatorV3Mock aggregator = new AggregatorV3Mock();

        aggregator.setData(1, 1000000000000000000, 1, block.timestamp, 1);

        oracles[0] = address(aggregator);

        vm.startPrank(admin);
        oracle.setChainlinkOracles(address(vault), tokens, _convert(oracles));
        vm.stopPrank();

        aggregator.setData(1, 0, 1, block.timestamp, 1);

        //vm.expectRevert(abi.encodeWithSignature("InvalidOracleData()"));
        (uint256 answer, ) = oracle.getPrice(address(vault), tokens[0]);
        assertEq(answer, 0);
    }
```
## Code Snippet
https://github.com/sherlock-audit/2024-06-mellow/blob/main/mellow-lrt/src/oracles/ChainlinkOracle.sol#L65
## Tool used
Manual Review

## Recommendation
Fix it as bellow:
```diff
    function _validateAndGetPrice(
        AggregatorData memory data
    ) private view returns (uint256 answer, uint8 decimals) {
        if (data.aggregatorV3 == address(0)) revert AddressZero();
        (, int256 signedAnswer, , uint256 lastTimestamp, ) = IAggregatorV3(data.aggregatorV3).latestRoundData();
        // The roundId and latestRound are not used in the validation process to ensure compatibility
        // with various custom aggregator implementations that may handle these parameters differently
-       if (signedAnswer < 0) revert InvalidOracleData();
+       if (signedAnswer <= 0) revert InvalidOracleData();
        answer = uint256(signedAnswer);
        if (block.timestamp - data.maxAge > lastTimestamp) revert StaleOracle();
        decimals = IAggregatorV3(data.aggregatorV3).decimals();
    }
```