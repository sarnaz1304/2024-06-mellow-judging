Droll Ash Nuthatch

High

# DefaultBondStrategy::depositCallback should not deposit the entire tvl

## Summary

Vault depositors can grief both withdrawals because `depositCallback` always deposits the entire Vault balance, instead of only the new deposit.

## Vulnerability Detail

When Vault has a callback address configured, all the deposits will end up calling the `depositCallback` that takes the **entire balance** of the Vault, so it can be further deposited in the bonds in order to receive yield:

```solidity
function depositCallback(uint256[] memory, uint256) external override {
    if (msg.sender != address(vault)) _requireAtLeastOperator();
    _deposit();
}

function _deposit() private {
    ITvlModule.Data[] memory tvl = erc20TvlModule.tvl(address(vault));
    for (uint256 i = 0; i < tvl.length; i++) {
        address token = tvl[i].token;
        bytes memory data_ = tokenToData[token];
        if (data_.length == 0) continue;
        Data[] memory data = abi.decode(data_, (Data[]));
        for (uint256 j = 0; j < data.length; j++) {
            uint256 amount = FullMath.mulDiv(
                tvl[i].amount,
                data[j].ratioX96,
                Q96
            );
            if (amount == 0) continue;
            vault.delegateCall(
                address(bondModule),
                abi.encodeWithSelector(
                    IDefaultBondModule.deposit.selector,
                    data[j].bond,
                    amount
                )
            );
        }
    }
```

Here we can see that `DefaultBondModule::deposit` will use the `amount` received from `Erc20TvlModule::tvl`, instead of the amount that is currently being deposited, leaving the others tokens idling in the `Vault` for withdrawals:

```solidity
contract ERC20TvlModule is IERC20TvlModule, DefaultModule {
    /// @inheritdoc ITvlModule
    function tvl(
        address vault
    ) external view noDelegateCall returns (Data[] memory data) {
        address[] memory tokens = IVault(vault).underlyingTokens();
        data = new Data[](tokens.length);
        for (uint256 i = 0; i < tokens.length; i++) {
            data[i].token = tokens[i];
            data[i].underlyingToken = tokens[i];
            data[i].amount = IERC20(tokens[i]).balanceOf(vault);
            data[i].underlyingAmount = data[i].amount;
        }
    }
}
```

Knowing that malicious user can execute deposits the following issues will occur, depending on which function is being frontrunned:

- `emergencyWithdraw`
    - frontrunning emergency withdrawal when there is a **single underlying token** and every deposit to bond takes all the tokens from the Vault, will either revert the transaction if there are non-zero `minAmounts` specified:
    
    ```solidity
        function emergencyWithdraw(
            uint256[] memory minAmounts,
            uint256 deadline
        )
            external
            nonReentrant
            checkDeadline(deadline)
            returns (uint256[] memory actualAmounts)
        {
    ...MORE CODE
            (address[] memory tokens, uint256[] memory amounts) = baseTvl();
            if (minAmounts.length != tokens.length) revert InvalidLength();
            actualAmounts = new uint256[](tokens.length);
            for (uint256 i = 0; i < tokens.length; i++) {
                if (amounts[i] == 0) {
                    if (minAmounts[i] != 0) revert InsufficientAmount();
                    continue;
                }
                uint256 amount = FullMath.mulDiv(
                    IERC20(tokens[i]).balanceOf(address(this)),
                    request.lpAmount,
                    totalSupply
                );
                if (amount < minAmounts[i]) revert InsufficientAmount();
                IERC20(tokens[i]).safeTransfer(request.to, amount);
                actualAmounts[i] = amount;
            }
            delete _withdrawalRequest[sender];
            _pendingWithdrawers.remove(sender);
            _burn(address(this), request.lpAmount);
            emit EmergencyWithdrawal(sender, request, actualAmounts);
        }
    ```
    
    - or in case user specifies no min amount, i.e. he is willing to lose some tokens instead he ends up receiving 0 tokens back because of the `deposit` transaction executed before has deposit the entire Vault balance.
- `Vault::processWithdrawals`
    - if `processWithdrawals` is called directly from the `Vault`, not from the `DefaultBondStrategy`, withdrawal from all the supported `Bonds` will not be executed:
    
    ```solidity
    function calculateStack()
        public
        view
        returns (ProcessWithdrawalsStack memory s)
    {
        (address[] memory tokens, uint256[] memory amounts) = underlyingTvl();//@audit amounts are zero 
        s = ProcessWithdrawalsStack({
            tokens: tokens,
            ratiosX96: IRatiosOracle(configurator.ratiosOracle())
                .getTargetRatiosX96(address(this), false),
            erc20Balances: new uint256[](tokens.length),
            totalSupply: totalSupply(),
            totalValue: 0,
            ratiosX96Value: 0,
            timestamp: block.timestamp,
            feeD9: configurator.withdrawalFeeD9(),
            tokensHash: keccak256(abi.encode(tokens))
        });
    
        IPriceOracle priceOracle = IPriceOracle(configurator.priceOracle());
        for (uint256 i = 0; i < tokens.length; i++) {
            uint256 priceX96 = priceOracle.priceX96(address(this), tokens[i]);
            s.totalValue += FullMath.mulDiv(amounts[i], priceX96, Q96);//@audit this is zero 
            s.ratiosX96Value += FullMath.mulDiv(s.ratiosX96[i], priceX96, Q96);
            s.erc20Balances[i] = IERC20(tokens[i]).balanceOf(address(this));//@audit this is zero 
        }
    }
    ```
    
    Frontrunning `processWithdrawals` with `deposit` will decrease all the token balances (**or even making it equal to 0, in case there is a single underlying token**), since all of them will be converted to Symbiotic LPTokens in the `DefaultBondModule`.
    
    Then `analyzeRequest` will **fail** because of the calculated underlying token amount being lower than the `minAmount` specified from withdrawal recipient. If we take the scenario with single underlying token, we will receive 0 amount from the `Vault::underlyingTvl` and this same amount is used to calculate the exchange rate of the lpTokens:
    
    ```solidity
    function analyzeRequest(
        ProcessWithdrawalsStack memory s,
        WithdrawalRequest memory request
    ) public pure returns (bool, bool, uint256[] memory expectedAmounts) {
        uint256 lpAmount = request.lpAmount;
        if (
            request.tokensHash != s.tokensHash || request.deadline < s.timestamp
        ) return (false, false, expectedAmounts);
    
        uint256 value = FullMath.mulDiv(lpAmount, s.totalValue, s.totalSupply);
        value = FullMath.mulDiv(value, D9 - s.feeD9, D9);
        uint256 coefficientX96 = FullMath.mulDiv(value, Q96, s.ratiosX96Value);
    
        uint256 length = s.erc20Balances.length;
        expectedAmounts = new uint256[](length);
        for (uint256 i = 0; i < length; i++) {
            uint256 ratiosX96 = s.ratiosX96[i];
            expectedAmounts[i] = ratiosX96 == 0
                ? 0
                : FullMath.mulDiv(coefficientX96, ratiosX96, Q96);
                //@audit less than directly cancels the request
            if (expectedAmounts[i] >= request.minAmounts[i]) continue;
            return (false, false, expectedAmounts);
        }
        for (uint256 i = 0; i < length; i++) {
            if (s.erc20Balances[i] >= expectedAmounts[i]) continue;
            return (true, false, expectedAmounts);
        }
        return (true, true, expectedAmounts);
    } 
    ```
    
    ```solidity
      function processWithdrawals(
          address[] memory users
      ) external nonReentrant returns (bool[] memory statuses) {
          _requireAtLeastOperator();
          statuses = new bool[](users.length);
          ProcessWithdrawalsStack memory s = calculateStack();
          uint256 burningSupply = 0;
          for (uint256 i = 0; i < users.length; i++) {
              address user = users[i];
              if (!_pendingWithdrawers.contains(user)) continue;
              WithdrawalRequest memory request = _withdrawalRequest[user];
              (
                  bool isProcessingPossible,
                  bool isWithdrawalPossible,
                  uint256[] memory expectedAmounts
              ) = analyzeRequest(s, request);
    
              if (!isProcessingPossible) {
              //@audit when insufficient expectedAmounts are calculated
                  _cancelWithdrawalRequest(user);
                  continue;
              }
    ```
    

## Impact

- griefing of withdrawals, excessive gas consumption for the Vault operators

## Code Snippet

https://github.com/mellow-finance/mellow-lrt/blob/dev-symbiotic-deploy/src/Vault.sol#L536-L556

## Tool used

Manual Review

## Recommendation

Possible approach is to invoke `depositCallback` only with the deposited amount, and second access controlled function to perform full deposits.