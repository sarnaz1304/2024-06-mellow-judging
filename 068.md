Scrawny Carbon Ape

Medium

# User can continue to earn points during withdrawal lockup period.

## Summary
User can continue to earn points during withdrawal lockup period.

## Vulnerability Detail
Lockup period is between `registerWithdrawal` and `processWithdrawals` functions.
During this period, user should lockup `lpAmout` of LRT to withdraw and cannot earn points for them at all.
`Vault.sol#registerWithdrawal` function is the following.
```solidity
    function registerWithdrawal(
        address to,
        uint256 lpAmount,
        uint256[] memory minAmounts,
        uint256 deadline,
        uint256 requestDeadline,
440:    bool closePrevious
    )
        external
        nonReentrant
        checkDeadline(deadline)
        checkDeadline(requestDeadline)
    {
        uint256 timestamp = block.timestamp;
        address sender = msg.sender;
        if (_pendingWithdrawers.contains(sender)) {
            if (!closePrevious) revert InvalidState();
451:        _cancelWithdrawalRequest(sender);
        }
        uint256 balance = balanceOf(sender);
        if (lpAmount > balance) lpAmount = balance;
        if (lpAmount == 0) revert ValueZero();
        if (to == address(0)) revert AddressZero();

        address[] memory tokens = _underlyingTokens;
        if (tokens.length != minAmounts.length) revert InvalidLength();

        WithdrawalRequest memory request = WithdrawalRequest({
            to: to,
            lpAmount: lpAmount,
            tokensHash: keccak256(abi.encode(tokens)),
            minAmounts: minAmounts,
            deadline: requestDeadline,
            timestamp: timestamp
        });
        _withdrawalRequest[sender] = request;
        _pendingWithdrawers.add(sender);
471:    _transfer(sender, address(this), lpAmount);
        emit WithdrawalRequested(sender, request);
    }
```
As can be seen, user should lockup `lpAmount` of LRT in `L471`.
During lockup period, an operator should perform operations to ensure that there are enough `underlyingTokens` on the vault's balance and then call the following `processWithdrawals` function.
```solidity
    function processWithdrawals(
        address[] memory users
    ) external nonReentrant returns (bool[] memory statuses) {
        _requireAtLeastOperator();
        statuses = new bool[](users.length);
        ProcessWithdrawalsStack memory s = calculateStack();
        uint256 burningSupply = 0;
        for (uint256 i = 0; i < users.length; i++) {
            address user = users[i];
            if (!_pendingWithdrawers.contains(user)) continue;
            WithdrawalRequest memory request = _withdrawalRequest[user];
            (
                bool isProcessingPossible,
                bool isWithdrawalPossible,
                uint256[] memory expectedAmounts
            ) = analyzeRequest(s, request);

            if (!isProcessingPossible) {
                _cancelWithdrawalRequest(user);
                continue;
            }

            if (!isWithdrawalPossible) continue;

            for (uint256 j = 0; j < s.tokens.length; j++) {
                s.erc20Balances[j] -= expectedAmounts[j];
                IERC20(s.tokens[j]).safeTransfer(
                    request.to,
                    expectedAmounts[j]
                );
            }

            burningSupply += request.lpAmount;
            delete _withdrawalRequest[user];
            _pendingWithdrawers.remove(user);
            statuses[i] = true;
        }

        if (burningSupply == 0) return statuses;
        _burn(address(this), burningSupply);
        emit WithdrawalsProcessed(users, statuses);

        address callback = configurator.withdrawalCallback();
        if (callback == address(0)) return statuses;
        IWithdrawalCallback(callback).withdrawalCallback();
        emit WithdrawCallback(callback);
    }
```
In the above function, operator processes batch of users' withdrawal requests, so if there are not sufficient underlying assets in the vault, withdraw requests of some users will be skipped.

On the other hand, since operator uses only users' addresses as parameters, if a user calls `registerWithdrawal` with `closePrevious = true`(`L440`) just before the operator's process of withdrawals, the previous request will be canceled (`L451`) and the new request will be registered and processed instantly by the operator.
Such cases may arise by intentional front-run or by chance.

Example:
1. A User calls `registerWithdrawal` with extremely small `lpAmount`(ex: `1`).
2. Some other users also call `registerWithdrawal` and lockup their LRTs in order to withdraw some underlying assets.
3. An operator collects users' addresses to withdraw, prepares sufficient underlying assets and then calls `processWithdrawals`.
4. User detects it from the mempool and call the `registerWithdrawal` as front-run with sufficiently large `lpAmount`.
As a result, the user withdraw large amount of underlying assets without lockup at all.
4. The withdrawal requests of other users processed after the user will be skipped for there is lack of underlying assets. 

Note:
In general, users must lockup their LRTs for unexpected periods in order to withdraw underlying assets.
Although a user make a withdrawal request just before the operator's processing, he will not be included instantly in the list to be processed because the operator has not prepared underlying assets for the user and it must take some time to prepare them.
But the user who use front-run can avoid this lockup period as shown above.

## Impact
User can continue to earn points during withdrawal lockup period.
In other words, user can withdraw underlying assets without lockup of LRTs.

Rather, some other users who lockup their LRTs for lockup period will be skipped due to the lacks of underlying assets.
They should wait until next processing of withdrawal requests and even may be canceled by deadline condition.

## Code Snippet
https://github.com/sherlock-audit/2024-06-mellow/tree/main/mellow-lrt/src/Vault.sol#L434-L478
https://github.com/sherlock-audit/2024-06-mellow/tree/main/mellow-lrt/src/Vault.sol#L536-L582

## Tool used

Manual Review

## Recommendation
Introduce `minLockupPeriod` parameter and modify the `Vault.sol#processWithdrawals` function as follows.
```solidity
    function processWithdrawals(
        address[] memory users
    ) external nonReentrant returns (bool[] memory statuses) {
        _requireAtLeastOperator();
        statuses = new bool[](users.length);
        ProcessWithdrawalsStack memory s = calculateStack();
        uint256 burningSupply = 0;
        for (uint256 i = 0; i < users.length; i++) {
            address user = users[i];
            if (!_pendingWithdrawers.contains(user)) continue;
            WithdrawalRequest memory request = _withdrawalRequest[user];
++          if (request.timestamp + minLockupPeriod > block.timestamp) continue;
            (
                bool isProcessingPossible,
                bool isWithdrawalPossible,
                uint256[] memory expectedAmounts
            ) = analyzeRequest(s, request);

            if (!isProcessingPossible) {
                _cancelWithdrawalRequest(user);
                continue;
            }

            if (!isWithdrawalPossible) continue;

            for (uint256 j = 0; j < s.tokens.length; j++) {
                s.erc20Balances[j] -= expectedAmounts[j];
                IERC20(s.tokens[j]).safeTransfer(
                    request.to,
                    expectedAmounts[j]
                );
            }

            burningSupply += request.lpAmount;
            delete _withdrawalRequest[user];
            _pendingWithdrawers.remove(user);
            statuses[i] = true;
        }

        if (burningSupply == 0) return statuses;
        _burn(address(this), burningSupply);
        emit WithdrawalsProcessed(users, statuses);

        address callback = configurator.withdrawalCallback();
        if (callback == address(0)) return statuses;
        IWithdrawalCallback(callback).withdrawalCallback();
        emit WithdrawCallback(callback);
    }
```