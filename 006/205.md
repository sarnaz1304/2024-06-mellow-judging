Droll Ash Nuthatch

High

# Vault::delegateCall doesn’t check for execution success

## Summary

`delegateCall` execution `success` is not checked.

## Vulnerability Detail

Since `Vault:delegateCall` is used multiple times, and mostly the call data is a function interacting with external calls, it's common for each of these calls to revert is any cases and thus the `success` of the `delegateCall` to be `false`. But this is unchecked, which will skip failure and continue executing the function.

```solidity
function delegateCall(
    address to,
    bytes calldata data
) external returns (bool success, bytes memory response) {
    _requireAtLeastOperator();
    if (!configurator.isDelegateModuleApproved(to)) revert Forbidden();
    IValidator validator = IValidator(configurator.validator());
    validator.validate(
        msg.sender,
        address(this),
        abi.encodeWithSelector(msg.sig, to, data)
    );
    validator.validate(address(this), to, data);
    (success, response) = to.delegatecall(data); // @audit success not checkeds
    emit DelegateCall(to, data, success, response);
}
```

Let’s consider the example when `StakingModule::convert` which is called through `vault::delegateCall` fail and the  `SimpleDVTStakingStrategy::processWithdrawals` continues its execution but without increasing the `wstETH` balance. 

Then `vault::calculateStack` and especially `s.totalValue` will be lowered, since the weth has not been able to be converted to `wstETH`.

```solidity
  function calculateStack()
      public
      view
      returns (ProcessWithdrawalsStack memory s)
  {
      (address[] memory tokens, uint256[] memory amounts) = underlyingTvl();//tokens[0] = wstETH
     ...MORE CODE
      IPriceOracle priceOracle = IPriceOracle(configurator.priceOracle());
      for (uint256 i = 0; i < tokens.length; i++) {
          uint256 priceX96 = priceOracle.priceX96(address(this), tokens[i]);
          s.totalValue += FullMath.mulDiv(amounts[i], priceX96, Q96);//wstETH.balanceOf(vault) < weth.balanceOf(vault)
          s.ratiosX96Value += FullMath.mulDiv(s.ratiosX96[i], priceX96, Q96);
          s.erc20Balances[i] = IERC20(tokens[i]).balanceOf(address(this));
      }
  }
```

And from there, all the users who specified tighter slippage will have their requests cancelled due to insufficient `expectedAmount`

```solidity
function analyzeRequest(
  ProcessWithdrawalsStack memory s,
  WithdrawalRequest memory request
) public pure returns (bool, bool, uint256[] memory expectedAmounts) {
  uint256 lpAmount = request.lpAmount;
  if (
      request.tokensHash != s.tokensHash || request.deadline < s.timestamp
  ) return (false, false, expectedAmounts);

  uint256 value = FullMath.mulDiv(lpAmount, s.totalValue, s.totalSupply);
  value = FullMath.mulDiv(value, D9 - s.feeD9, D9);
  uint256 coefficientX96 = FullMath.mulDiv(value, Q96, s.ratiosX96Value);

  uint256 length = s.erc20Balances.length;
  expectedAmounts = new uint256[](length);
  for (uint256 i = 0; i < length; i++) {
      uint256 ratiosX96 = s.ratiosX96[i];
      expectedAmounts[i] = ratiosX96 == 0
          ? 0
          : FullMath.mulDiv(coefficientX96, ratiosX96, Q96);
      if (expectedAmounts[i] >= request.minAmounts[i]) continue;//@audit
      return (false, false, expectedAmounts);
  }
  for (uint256 i = 0; i < length; i++) {
      if (s.erc20Balances[i] >= expectedAmounts[i]) continue;
      return (true, false, expectedAmounts);
  }
  return (true, true, expectedAmounts);
}
```

This is only one of all the possible scenarios that will happen in case `delegateCall` silently fails.

## Impact

If the function called via `delegatecall` fails, execution of `Vault:delegateCall` will continue, assuming the call succeeded.

## Code Snippet

https://github.com/sherlock-audit/2024-06-mellow/blob/26aa0445ec405a4ad637bddeeedec4efe1eba8d2/mellow-lrt/src/Vault.sol#L280

https://github.com/mellow-finance/mellow-lrt/blob/dev-symbiotic-deploy/src/Vault.sol#L529

https://github.com/mellow-finance/mellow-lrt/blob/dev-symbiotic-deploy/src/Vault.sol#L476C1-L504C6

## Tool used

Manual Review

## Recommendation

Add check if `success` is true.